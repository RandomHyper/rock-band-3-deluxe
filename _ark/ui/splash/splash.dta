#define DX_SPLASH_PANEL_HANDLES
(
   (lbl_maker
      ($type $obj $x $y $align $size $w $h $colorname $r $g $b $content)
      {new $type $obj}
      {$obj set resource_name "pentatonic_outline"}
      {$obj set alt_style_enabled TRUE}
      {$obj set alt_font_resource_name pentatonic_outline}
      {if {! {exists $colorname}}
         {new UIColor $colorname}
         {$colorname set color {pack_color $r $g $b}}
      }
      {$obj set markup TRUE}
      {$obj set_showing TRUE}
      {$obj set_local_scale 1 1 1}
      {$obj set_local_pos $x 0 $y}
      {if {== $type BandLabel}
         {$obj set_token_fmt $content}
      }
      {if {== $type BandButton}
         {$obj set text_token $content}
      }
      {$obj set text_size $size}
      {$obj set alt_text_size 15}
      {$obj set alt_z_offset -4}
      {$obj set fit_type 2}
      {$obj set alignment $align}
      {$obj set width $w}
      {$obj set height $h}
      {$obj set alpha 1}
      {$obj set color_override $colorname}
      {$obj set alt_text_color $colorname}
      {text.grp add_object $obj}
   )
   (dx_splash_panel_enter
      {do
         ($num {random_int 0 500})
         ($pass
            #ifdef _SHIP
            {&& {== $num 69} $debug_mode} ;nice
            #else
            FALSE ;don't do in debug
            #endif
         )
         {if_else $pass
            {$this beta_text $num}
            {$this splash_text} ;also handles logo and printing build
         }
      }

      {dx_optimize_splash {{sv8_panel find sv8_a} find cityscape_ao}}

      ;we are going to steal THE MOON
      {{{{sv8_panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 0 2.5}
      {{{{sv8_panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 2 1.3}
   )
   (splash_text
      {do
         ($num 179) ;increment this number based on the number of dx_splash entries in locale
         {$this lbl_maker
            BandButton ;type
            dx_splashtext.btn ;name
            197.5 -140 ;x/y position
            kMiddleCenter ;alignment
            30 ;text size
            400 30 ;width/height
            splash.color ;color name
            0.96 0.90 0.28 ;rgb color
            {sprint "dx_splash_" {random_int 1 $num}}
         }

         {start.btn set_showing FALSE}
         {dx_splashtext.btn set_local_rot 35 12.5 -25}

         {new PropAnim splash_text.anim}
         {splash_text.anim add_keys dx_splashtext.btn (position) kPropVector3}

         {splash_text.anim set_key_val dx_splashtext.btn (position) 0 (197.5 0 140)}
         {splash_text.anim set_key_val dx_splashtext.btn (position) 1 (197.5 -50 140)}
         {splash_text.anim set_key_val dx_splashtext.btn (position) 2 (197.5 0 140)}
         {splash_text.anim set_interp_type dx_splashtext.btn (position) kPropHermite}

         {splash_text.anim set rate kTaskUISeconds}
         {splash_text.anim set_frame 0}
         {splash_text.anim animate (loop 0 2) (period 1) (units kTaskUISeconds)}
      }
      ;call the rest of the flow (not done if beta text)
      {$this set_logo}
      {$this print_build dx_build.lbl}
   )
   (beta_text
      ($num)
      {$this lbl_maker
         BandButton ;type
         dx_splashtext.btn ;name
         0 63 ;x/y position
         kMiddleCenter ;alignment
         22.5 ;text size
         600 30 ;width/height
         beta.color ;color name
         1 0 0 ;rgb color
         os_betaegg
      }
      {{{{sv8_panel find sv8_a} find cityscape_ao} find logo.mesh} set_showing FALSE}
      {rb3.lbl set_showing TRUE}
      {if {>= $num 34}
         {rb3.lbl set_token_fmt os_rb3dx}
      }
   )
   (print_build
      ($lbl)
      ;setup build label in case GoCentral is overriding our motd
      {if {! {exists $lbl}}
         {$this lbl_maker
            BandLabel ;type
            $lbl ;name
            395 -215 ;x/y position
            kBottomRight ;alignment
            15 ;text size
            1000 40 ;width/height
            build.color ;color name
            1 1 1 ;rgb color
            rb3e_mod_string
         }
      }
      {dx_buildversion_label $lbl}
   )
   (set_logo
      {do
         ($root "dx/custom_textures/_additional_textures/")
         {set_this {{sv8_panel find sv8_a} find cityscape_ao}}
         ; set main menu logo based on season
         {if_else $dx_seasonal_logos
            {do
               {get_date_time $year $month $day $hour $minute $second}
               {cond
                  ({&& {== $month 2} {>= $day 7} {<= $day 17}} {set $dx_season valentines})
                  ({&& {== $month 4} {== $day 1}} {set $dx_season aprilfools})
                  ({== $month 6} {set $dx_season pride})
                  ({== $month 10} {set $dx_season halloween})
                  ({== $month 12} {set $dx_season christmas})
                  (else {set $dx_season standard})
               }
            }
            {set $dx_season standard}
         }
         {logo.tex set_bitmap {sprint $root "logo-" $dx_season #ifdef RB3E "-rb3e" #endif ".png"}}
         {if {== $dx_season halloween}
            {moon.tex set_bitmap {sprint $root "blood_moon.png"}}
         }
         {if {== $dx_season christmas}
            {dx_get_festive {{sv8_panel find sv8_a} find cityscape_ao}}
         }
      }
   )
   (dx_splash_panel_exit
      {foreach $entry (animate_splash dx_build.lbl custom.color splash_text.anim dx_splashtext.btn)
         {if {exists $entry}
            {delete $entry}
         }
      }
   )
)
#define kSplashScreen_Entered (0)
#define kSplashScreen_ActivateSaveLoad (1)
#define kSplashScreen_StartOvershell (2)
#define kSplashScreen_WaitOvershell (3)
#define kSplashScreen_EndOvershell (4)
#ifdef HX_WII
#define kSplashScreen_ImportFriends (6)
#define kSplashScreen_PreEndOvershell (7)
#define kSplashScreen_CheckFirstInstrument (8)
#define kSplashScreen_WaitPushScreen (9)
#endif
#ifdef HX_WII
{func get_controller_type
   ($con_type)
   {switch $con_type
      (kJoypadWiiCore "vocals")
      (kJoypadWiiFS "vocals")
      (kJoypadWiiGuitar "guitar")
      (kJoypadWiiHxGuitar "guitar")
      (kJoypadWiiHxGuitarRb2 "guitar")
      (kJoypadWiiCoreGuitar "guitar")
      (kJoypadWiiDrums "drum")
      (kJoypadWiiHxDrums "drum")
      (kJoypadWiiHxDrumsRb2 "drum")
      (kJoypadWiiKeytar "keyboard")
      (kJoypadWiiButtonGuitar "madcatz")
      (kJoypadWiiRealGuitar22Fret "squire")
      (kJoypadWiiMidiBoxKeyboard "midi")
      (kJoypadWiiMidiBoxDrums "midi")
      "vocals"
   }
}
#endif
{new MoviePanel
   intro_movie_panel
   (preload FALSE)
   (audio TRUE)
   (loop FALSE)
   (videos
      {switch $dx_current_intro_movie
         ((deluxe disabled) #ifdef HX_WII dx_intro_movie_wii #else dx_intro_movie #endif)
         (vanilla intro_movie)
         intro_movie
      }
   )
   (should_leave FALSE)
   (should_leave_count 0)
   (wii_go_to_main
      ($screen)
      {if_else {content_mgr is_sdcard_inserted}
         {do
            {startup_sdmode_screen set final_screen splash_screen}
            {ui goto_screen startup_sdmode_screen}
         }
         {ui goto_screen $screen}
      }
   )
   (leave
      ($screen)
      #ifdef _SHIP
      {if {! [should_leave]}
         {saveload_mgr show_save_icon}
      }
      {set [should_leave] TRUE}
      #else
      {$this wii_go_to_main $screen}
      #endif
   )
   (dx_movie_enter
      {do
         ($screen
            {if_else #ifndef HX_WII {&& {! $dx_black_menu} $dx_splash_screen} #else TRUE #endif ;always go to splash if wii
               splash_screen ; dx - goes to the splash screen if enabled
               meta_loading_main_screen ; dx - skips the intro movie instantly if disabled
            }
         )
         {saveload_mgr activate} ; dx - load save file early
         #ifdef HX_PS3
         {content_mgr start_refresh} ; dx - start content enumeration early, this causes cache issues on 360 so ps3 only
         #endif
         {profile_mgr set_overscan TRUE}
         #ifdef HX_WII
         {$this leave $screen}
         #else
         {ui goto_screen $screen}
         #endif
      }
   )
   (enter
      {rnd set_sync 1}
      ;{rnd toggle_heap}
      {if {== $dx_current_intro_movie disabled}
         {$this dx_movie_enter}
      }
      {if {!= $dx_current_intro_movie disabled}
         #ifdef HX_PS3
         ; load stuff early, these cause issues on 360 so ps3 only
         ;{saveload_mgr activate}
         {content_mgr start_refresh}
         #endif
         {profile_mgr set_overscan TRUE}
      }
   )
   (movie_done {$this dx_movie_enter})
   (enter
      {set [should_leave] FALSE}
      {set [should_leave_count] 0}
      {platform_mgr disable_xmp}
      kDataUnhandled
   )
   (exit
      {rnd set_sync $dx_vsync}
      #ifdef HX_PS3
      {if
         {elem
            {find $syscfg system trophies}
            1
         }
         {register_trophy}
      }
      #endif
   )
   (BUTTON_DOWN_MSG
      {if {'||' {== $action kAction_Start} {== $action kAction_Confirm}}
         {$this dx_movie_enter}
      }
   )
   #ifdef HX_WII
      #ifdef _SHIP
      (poll
         {if {== [should_leave_count] 20}
            {do
               {saveload_mgr create_vf_cache}
               {saveload_mgr hide_save_icon}
               {$this dx_movie_enter}
            }
         }
         {if [should_leave]
            {set [should_leave_count] {'+' [should_leave_count] 1}}
         }
      )
      #endif
   #endif
}
{new BandScreen
   intro_movie_helper
   (TRANSITION_COMPLETE_MSG {ui goto_screen intro_movie_screen})
}
{new BandScreen
   intro_movie_screen
   (panels intro_movie_panel)
   (focus intro_movie_panel)
   #ifdef HX_WII
   (block_char_cache TRUE)
   #endif
}
{new UIPanel
   splash_panel
   (file "splash.milo")
   (focus start.btn)
   (last_user '')
   (splash_state kSplashScreen_Entered)
   #ifdef HX_WII
   (show_online_restricted_message TRUE)
   #endif
   (load
      #ifdef HX_WII
      {set [splash_state] kSplashScreen_Entered}
      #endif
      {overshell set_active_status kOvershellInactive}
      {session clear}
   )
   DX_SPLASH_PANEL_HANDLES
   (enter
      #ifdef HX_WII
      {wiiprofile_panel req_delay}
      {if
         {&&
            {platform_mgr is_online_restricted}
            {$this get show_online_restricted_message}
         }
         {do
            {set [show_online_restricted_message] FALSE}
            {ui push_screen wii_online_restricted_screen}
         }
      }
      #endif
      {$this dx_splash_panel_enter}
      {loading.grp set_showing FALSE}
   )
   (exit {$this dx_splash_panel_exit})
   (TRANSITION_COMPLETE_MSG
      #ifdef HX_WII
      {if {== [splash_state] kSplashScreen_PreEndOvershell}
         {$this set_splash_state kSplashScreen_EndOvershell}
      }
      #endif
   )
   (poll
      {if
         {&&
            {! {ui in_transition}}
            {== {ui current_screen} splash_screen}
            #ifdef HX_PS3
            {trophy_registered}
            #endif
         }
         #ifndef HX_WII
         {cond
            #ifdef HX_PS3
            ({&&
                  {elem
                     {find $syscfg system trophies}
                     1}
                  {!
                     {trophy_disk_space_error_screen get displayed}}
                  {trophy_disk_space_error}}
               {ui push_screen trophy_disk_space_error_screen}
            )
            #endif
         }
         #endif
      }
      {if {== [splash_state] kSplashScreen_ActivateSaveLoad}
         {if {saveload_mgr is_idle}
            {$this set_splash_state kSplashScreen_StartOvershell}
         }
      }
   )
   (BUTTON_DOWN_MSG
      {cond
         #ifdef HX_PS3
         ({! {trophy_registered}} TRUE)
         #endif
         ({== $action kAction_Start}
            {$this button_down $user $raw_button kAction_Confirm $pad_num}
         )
         (TRUE kDataUnhandled)
      }
   )
   (SELECT_MSG
      {if {&& {! {ui in_transition}} {== {ui current_screen} splash_screen}}
         {if {== [splash_state] kSplashScreen_Entered}
            {set [last_user] $user}
            {$this set_splash_state kSplashScreen_ActivateSaveLoad}
         }
      }
   )
   (set_splash_state
      ($state)
      {set [splash_state] $state}
      {switch $state
         (kSplashScreen_ActivateSaveLoad
            {saveload_mgr activate}
            {text.grp set_showing FALSE}
            #ifdef HX_WII
            {loading.grp set_showing TRUE}
            #endif
         )
         (kSplashScreen_StartOvershell
            #ifdef HX_WII
            {loading.grp set_showing FALSE}
            #endif
            {main_hub_panel set first_user [last_user]}
            {overshell attempt_to_add_user [last_user]}
            {profile_mgr set_primary_profile_by_user [last_user]}
            {overshell set_active_status kOvershellInShell}
            {$this set_splash_state kSplashScreen_WaitOvershell}
            {overshell update_all}
         )
         #ifdef HX_WII
         (
            (kSplashScreen_CheckFirstInstrument)
            {overshell set_active_status kOvershellInactive}
            {if_else
               {'||'
                  {== {wiiprofile_panel is_active} TRUE}
                  {platform_mgr is_user_a_wiiguest [last_user]}
                  {! {profile_mgr get_has_seen_first_time_instruments [last_user]}}
               }
               {do
                  {set [splash_state] kSplashScreen_WaitPushScreen}
                  {if_else {== {wiiprofile_panel is_active} TRUE}
                     {set $type {{wiiprofile_panel get_user} get_pad_type}}
                     {set $type {{splash_panel get last_user} get_pad_type}}
                  }
                  {first_time_instrument_panel set controller_type $type}
                  {profile_mgr set_has_seen_first_time_instruments [last_user] TRUE}
                  {ui push_screen first_time_instrument_screen}
               }
               {do
                  {handle_type ($this set_splash_state kSplashScreen_EndOvershell)}
               }
            }
         )
	 (kSplashScreen_WiiFriendsPrompt
            {unless {ui_event_mgr has_transition_event go_to_wiifriends_screen}
               {do
                  {overshell set_active_status kOvershellInShell}
                  {wiifriends_screen go_to_import_friends_screen}
               }
            }
         )
         (
            (kSplashScreen_EndOvershell)
            {overshell set_active_status kOvershellInactive}
            {if_else {== {wiiprofile_panel is_active} TRUE}
               {do
                  {ui_event_mgr trigger_event
                     go_to_wiiprofilecreator
                     (dummy init [last_user])
                  }
                  {ui goto_screen main_hub_screen}
               }
               {do
                  {wiiprofile_panel clear_active}
                  {cond
                     ({!
                           {profile_mgr get_has_seen_first_time_calibration}}
                        {ui push_screen first_time_calibration}
                     )
                     {do
                        {if_else {profile_mgr get_should_show_wii_friends_prompt}
                           {handle_type ($this set_splash_state kSplashScreen_WiiFriendsPrompt)}
                           {do
                              {overshell set_active_status kOvershellInShell}
                              {ui goto_screen main_hub_screen}
                           }
                        }
                     }
                  }
               }
            }
         )
         #else
         (
            (kSplashScreen_EndOvershell)
            {cond
               ({!
                     {profile_mgr get_has_seen_first_time_calibration}}
                  {ui push_screen first_time_calibration}
               )
               {ui goto_screen main_hub_screen}
            }
         )
         #endif
      }
   )
   #ifdef HX_WII
   (overshell_allowing_input
      ($is_allowed $user)
      {if {&& {== $is_allowed TRUE} {== [splash_state] kSplashScreen_WaitOvershell}}
         {do
            {set [last_user] $user}
            {handle_type ($this set_splash_state kSplashScreen_CheckFirstInstrument)}
         }
      }
   )
   #else
   (overshell_allowing_input
      ($is_allowed)
      {if {== $is_allowed TRUE}
         {switch [splash_state]
            (kSplashScreen_WaitOvershell
               {handle_type ($this set_splash_state kSplashScreen_EndOvershell)}
            )
         }
      }
   )
   #endif
}
{new BandScreen
   splash_screen
   (panels meta sv8_panel splash_panel)
   (focus splash_panel)
   (gather_uistats FALSE)
}
#ifdef HX_PS3
{new BandScreen
   trophy_disk_space_error_screen
   (displayed FALSE)
   (panels dialog_panel)
   (focus dialog_panel)
   (enter
      {set [displayed] TRUE}
      {dialog_panel set_msg (trophy_disk_space_error {trophy_file_size_mb})}
   )
}
#endif
{new BandScreen
   first_time_calibration
   (panels dialog_small_panel)
   (focus dialog_small_panel)
   (enter
      {profile_mgr set_has_seen_first_time_calibration TRUE}
      {dialog_small_panel set_custom
         calibration_msg
         calibration_calibrate
         calibration_skip
         opt1.btn
      }
   )
   (SELECT_MSG
      {switch $component
         (opt1.btn
            {cal_welcome_screen set cancel_screen main_hub_screen}
            {cal_welcome_screen set confirm_screen main_hub_screen}
            {input_mgr set_user $user}
            {critical_user_listener set_critical_user $user}
            #ifdef HX_WII
            {overshell set_active_status kOvershellInShell}
            #endif
            {ui pop_screen cal_welcome_screen}
         )
         (opt2.btn {ui pop_screen main_hub_screen})
      }
   )
}
#ifdef HX_WII
{new UIPanel
   first_time_instrument_panel
   (controller_type none)
   (file
      {sprintf
         "wii_first_time_%s_keep.milo"
         {get_controller_type [controller_type]}
      }
      1
   )
   (focus ok.btn)
   (enter {enter.trg trigger})
   (exit {recoil.trg trigger})
}
{new BandScreen
   first_time_instrument_screen
   (panels first_time_instrument_panel)
   (focus first_time_instrument_panel)
   (SELECT_MSG
      {switch $component
         (ok.btn
            {splash_panel set_splash_state kSplashScreen_PreEndOvershell}
            {ui pop_screen}
         )
         kDataUnhandled
      }
   )
}
#endif
#ifdef HX_WII
{new BandScreen
   wii_online_restricted_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter
      {dialog_panel set_okcancel wii_error_wifi_enterpin no.btn}
      {platform_mgr print_parental_pin}
   )
   (SELECT_MSG
      {switch $component
         (no.btn {ui pop_screen})
         (yes.btn
            {virtual_keyboard show_keyboard
               $user
               4
               ""
               {localize store_parentalcontrol}
               ""
               $this
               kVkNumbersOnlyEntry
            }
         )
         kDataUnhandled
      }
   )
   (VIRTUAL_KEYBOARD_RESULT_MSG
      {if_else $ok
         {do
            {if_else {commerce_mgr set_parental_control_pin $text}
               {do
                  {platform_mgr relax_online_restriction}
                  {ui pop_screen}
               }
               {ui goto_screen splash_incorrect_pin_screen}
            }
         }
         {do
            {ui goto_screen splash_incorrect_pin_screen}
         }
      }
   )
}
{new BandScreen
   splash_incorrect_pin_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok store_incorrect_pin})
   (SELECT_MSG {ui goto_screen wii_online_restricted_screen})
}
#endif