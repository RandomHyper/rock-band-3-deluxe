#define DX_LOADING_HANDLES
(
   (dx_load_tip_and_author_finder
      {unless $dx_song_data_processed
         {set $dx_song_data_processed TRUE}
         {dx_author_finder_var_reset}
         {unless $dx_mtv_dta_reader_once
            {set $dx_mtv_dta_reader_once TRUE} ;no matter how many times this function gets called, we only need to read these dtas once because its very heavy
            {dx_mtv_reader}
         }
      }
   )
   (dx_loading_enter
      ;reset midi parser end of song shot addition gate to prevent camera spam
      ;i know this is bad and shouldnt be here
      {set $add_end_shot FALSE}
      {dx_countdown_var_reset}
      {dx_midi_parser_var_reset}
      {set $dx_song_data_processed FALSE}
      {$this dx_load_tip_and_author_finder}

      {if_else {! {session_mgr is_local}}
         {set $dx_delay 2}
         {if_else $dx_song_delay
            {set $dx_delay 2}
            {set $dx_delay 0}
         }
      }

      {set $speedmod_top $leaderspeed}

      {if $dx_prep_show_flow
         {set $dx_prep_show_flow FALSE}
      }
      {set $current_solo_highscore FALSE}
      {foreach $entry $highscore_tracker
         {if {== {elem $entry 0} {meta_performer song}}
            {set $current_solo_highscore {elem $entry 1}}
         }
      }
   )
   (dx_set_up_loading_tip_label
      {do
         ($tip_roll {random_int 1 214}) ;increment based on number of tips in dx_locale_tips.dta
         ($num {random_int 0 50})
         ($roll {&& $dx_loading_tip_var {>= $num 25}})
         ($str ;randomize the string for this tip
            {if_else $roll ;50% chance if we found a tip for this song
               {sprint $dx_loading_tip_var} ; set the loading tip found in songs.dta
               {localize {sprint "dx_loading_tip_" $tip_roll}} ; otherwise, set a random loading tip
            }
         )
         {set_this transition_fallback_panel}
         {loading.lbl set caps_mode kCapsModeNone} ; make label lowercase
         {loading.lbl set fixed_length 1000} ; increase the character count
         {loading.lbl set width 640} ; force a specific length
         {loading.lbl set fit_type kFitWrap} ; fit the text to the length and make a new line when reached
         {loading.lbl set alignment kTopCenter} ; keep the text below the loading emblem
         {loading.lbl set_local_pos_index 2 {- {loading.lbl get_locale_pos_index 2} 150}} ; set the y position to make it a lil prettier
         {loading.lbl set_token_fmt stringify $str} 
         {set $dx_loading_tip_var FALSE}
      }
   )
)
{new BandPreloadPanel
   preload_panel
   (song_mgr song_mgr)
   (current_song {meta_performer song})
   DX_LOADING_HANDLES
   (enter
      #ifndef HX_WII
      {$this dx_loading_enter}
      #endif
   )
   (preload_files
      ({song_mgr
            midi_file
            {meta_performer song}}
         FALSE
         #ifdef HX_PS3
         {song_mgr package_name {meta_performer song}}
         #endif
      )
      ({song_mgr
            song_file_path
            {meta_performer song}
            ".milo"}
         TRUE
         #ifdef HX_PS3
         ""
         #endif
      )
      ({song_mgr
            upgrade_midi_file
            {meta_performer song}}
         {! {song_mgr has_upgrade {meta_performer song}}}
         #ifdef HX_PS3
         {song_mgr upgrade_package_name {meta_performer song}}
         #endif
      )
   )
   #ifdef HX_WII
   (load
      {$this dx_loading_enter}
      {content_mgr set_passive_errors_enabled FALSE}
   )
   (finish_load {content_mgr set_passive_errors_enabled TRUE})
   #endif
   (on_preload_ok
      {net_sync disable}
      ;HEY BUD LISTEN UP
      ;I dont know why, i dont know how, but this fixes the rpcs3 crash when going to practice
      ;trying to check the gamemode makes it not work
      ;lowering the delay makes it not work
      ;ive tried several things
      ;its the L everyone has to take
      {do
         ($first {== {ui current_screen} preloading_screen})
         ($game #ifdef HX_WII pregame_screen #else {gamemode get game_screen} #endif)
         ($next {if_else $first $game load_nextsong_screen})
         {thread_task ;delay going to practice to let the game catch up
            kTaskUISeconds
            (delay $dx_delay)
            (script {ui goto_screen $next})
         }
      }
   )
   (on_preload_failed
      {net_sync disable}
      #ifdef HX_WII
      {if_else {== {ui current_screen} preloading_screen}
         {ui goto_screen wii_preloading_failed_screen}
         {ui goto_screen wii_preload_failed_nextsong_screen}
      }
      #else
      {passive_messenger trigger_skip_song_msg}
      {if_else {! {meta_performer is_last_song}}
         {if_else {== {ui current_screen} preloading_screen}
            {ui goto_screen preloading_failed_screen}
            {ui goto_screen preload_failed_nextsong_screen}
         }
         {ui goto_screen meta_loading_continue_screen}
      }
      #endif
   )
}
{new BandScreen
   preloading_screen
   (panels
      meta
      preload_panel
      #ifndef HX_WII
      transition_fallback_panel
      #endif
   )
   (focus preload_panel)
   #ifdef HX_XBOX
   (show_fallback $dx_xbox_show_fallback)
   #else
   (show_fallback TRUE)
   #endif
   (enter
      #ifndef HX_WII
      {transition_fallback_panel set_showing [show_fallback]}
      {{transition_fallback_panel find waiting.lbl} set_showing FALSE}
      {if [show_fallback]
         {transition_fallback_panel start_loading_quick_anim}
      }
      ;{$this set show_fallback FALSE}
      #endif
      {session_mgr set_active_roster TRUE}
      {overshell set_active_status kOvershellInactive}
      {seed_random_context {session_mgr get_net_random_seed}}
      {game set multi_event FALSE}
      {song_mgr add_recent_song {meta_performer song}}
      {preload_panel dx_load_tip_and_author_finder}
      #ifndef HX_WII
      {preload_panel dx_set_up_loading_tip_label}
      #endif
      {if {exists game}
         {acc_mgr clear_goal_info}
      }
   )
   (TRANSITION_COMPLETE_MSG
      {net_sync disable}
      #ifdef HX_WII
      {meta_performer load_festival}
      #endif
      kDataUnhandled
   )
}
{new BandScreen
   preloading_failed_screen
   (panels meta)
   (TRANSITION_COMPLETE_MSG
      {meta_performer skip_song}
      {ui goto_screen preloading_screen}
   )
}
#ifdef HX_WII
#define WII_PRELOADING_FAILED_HANDLERS (
   (focus dialog_panel)
   (error_msg '')
   (TRANSITION_COMPLETE_MSG
      {do
         ($data {song_mgr song_name {meta_performer song}})
         ($error {content_mgr get_last_result})
         {switch $error
            (kOpMissingCard {set $error_msg dlc_restore_sdcardremoved})
            (kOpCorruptData {set $error_msg (dlc_restore_corrupt_name $data)})
            (kOpCorruptCard {set $error_msg (dlc_restore_corrupt_name $data)})
            (kOpOutOfDate {set $error_msg (dlc_restore_outofdate $data $data)})
            (kOpNoLicense {set $error_msg (dlc_restore_nolicense $data $data)})
            (kOpIncompatibleCard {set $error_msg dlc_restore_incompatible})
            (kOpCorruptContent {set $error_msg dlc_restore_corrupt_content})
            (kOpFail {set $error_msg (dlc_restore_failed_name $data)})
            {if_else {! {session_mgr is_local}}
               {set $error_msg (wii_error_generic)}
               {set $error_msg (dlc_restore_failed_name $data)}
            }
         }
         {dialog_panel set_ok $error_msg}
      }
   )
)
{new BandScreen
   wii_preloading_failed_screen
   (panels meta dialog_panel)
   WII_PRELOADING_FAILED_HANDLERS
   (SELECT_MSG {ui goto_screen meta_loading_continue_screen})
}
#endif