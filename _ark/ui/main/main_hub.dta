#define DX_MAIN_HANDLES
(
   ;this is the main non-vanilla call flow for entering the main menu (non PaS)
   ;please put anything new in here
   (dx_main_on_enter
      ;dx does not use bns modifier, if its set in the save, disable and calculate track speed equivalent 
      {$this dx_correct_bns}

      #ifdef HX_XBOX
      {$this dx_warn_cache_expired}
      #endif

      #ifdef HX_PS3
      {$this dx_missing_highmem_check}
      {$this dx_missing_tele_patch}
      #endif

      #ifndef HX_WII
      DX_SET_SERVER_LOCALE ; dx - change button and label text when connected to gocentral
      {dx_presence_manager}
      #endif

      {dx_sv_menu_logo_set sv3_panel}

      {if_else $dx_event_mode
         {$this dx_event_setup}
         {$this dx_roll_prompts}
      }

      {$this dx_buildver_lbl}
      
      {dx_admin_mode}

      {if {modifier_mgr is_modifier_active mod_auto_play}
         {$this disable qp_fmp.btn}
         {$this disable tour_fmp.btn}
         {$this set_focus mb_playnow.btn}
      }
   )
   (dx_event_setup
      ;show welcome
      {unless {&& $dx_event_type $dx_event_welcome_screen_seen}
         {ui push_screen dx_EventModeWelcome_screen}
      }

      ;force set some modifiers
      {enable_modifier mod_no_fail_band}
      {enable_modifier mod_auto_vocals}
      ;force DISABLE drum navigation (people like to fuck with the drums and i know from experience)
      ;idk why this was commented out
      ;{disable_modifier mod_drum_surface_navigation}
      {disable_modifier mod_drum_fills}
      {enable_modifier mod_independent_track_speeds}
      {enable_modifier enable_popup_help}

      ;setup the main menu
      {menu_buttons.grp set_local_pos -350 0 -90}

      {do
         ($lbl event.lbl)
         ($align kTopLeft)
         ($event {if_else $dx_event_name $dx_event_name "the party"})
         {if {! {exists $lbl}}
            {switch $dx_event_type
               (party kDataUnhandled)
               (club
                  {$this lbl_maker $lbl -30 -30 $align os_blnk}
                  {$lbl set_local_scale 1.25 1.25 1.25}
                  {$lbl set_token_fmt dx_event_club_lbl}
               )
               (convention
                  {$this lbl_maker $lbl -30 -17 $align os_blnk}
                  {$lbl set_local_scale 2 2 2}
                  {$lbl set_token_fmt stringify
                     {sprint
                        {localize dx_welcome_to}
                        $event
                        {localize dx_event_convention_lbl}
                     }
                  }
               )
            }
         }
      }

      {do
         ($ev_lbl event_button_desc.lbl)
         {if {! {exists $ev_lbl}}
            {$this lbl_maker $ev_lbl -367 -150 kBottomLeft os_blnk}
            {$ev_lbl set_local_scale 1.25 1.25 1.25}
            {$ev_lbl set width 10000}
         }
         {$ev_lbl set_token_fmt dx_freeplay_desc}
         {messages.grp set_showing FALSE}
      }

      {$this iterate BandButton $l
         {if
            {&&
               {!= $l {sprint mb_playnow.btn}}
               {!= $l {sprint mb_career.btn}}
            }
            {$this disable $l}
            {$l set_showing FALSE}
         }
      }
      {mb_playnow.btn set text_token dx_freeplay}
      {mb_career.btn set text_token dx_play_a_show}
   )

   ;warning checks
   (dx_warn_cache_expired
      ;warn if out of date cache
      {if
         {&&
            {! $dx_cache_warn_seen}
            {!= $dx_song_update_hash $dx_song_update_hash_old}
         }
         {script_task kTaskSeconds
            (delay 1)
            (script
               {ui push_screen dx_cache_warn_screen}
            )
         }
      }
   )
   (dx_missing_highmem_check
      {if {== $dx_detected_platform platform_rpcs3}
         {if
            {||
               {! {file_exists DX_HI_MEM_PATH}}
               {! {> {size {read_file DX_HI_MEM_PATH}} 0}}
               {!
                  {== {elem {find {read_file DX_HI_MEM_PATH} dx_high_memory} 1} 190000000}
               }
            }
            {if {! $memwarned}
               {set $memwarned TRUE}
               {dx_modal_messenger dx_missing_highmem}
            }
         }
      }
   )
   (dx_missing_tele_patch
      {if
         {||
            {== $dx_detected_platform platform_rpcs3}
            {== $dx_detected_platform platform_ps3}
         }
         {if
            ;also has bass_patch.dtb, guitar_patch.dtb
            ;mic_patch.dtb, and hair_patch.dtb
            ;uncommented is latest we only check for now
            {! {file_exists "dx/gen/mic_patch_5.dtb"}}
            {if {! $telewarned}
               {set $telewarned TRUE}
               {dx_modal_messenger dx_missing_tele_patch}
            }
         }
      }
   )
   (dx_check_first_time
      #ifndef HX_WII
      {if_else
         {&&
            {! $dx_event_mode}
            {! {file_exists DX_SETTINGS_FILE_PATH}}
            {! $dx_welcome_seen}
         }
         {do ;you're welcome
            {profile_mgr set_voice_chat_volume 0}
            {dx_log_writer info {sprint "Delxue Settings not found, showing welcome screen"}}
            {set $dx_welcome_seen TRUE}
            {ui push_screen dx_welcome_screen}
         }
         {$this check_rewards_and_hints}
      }
      #endif
   )
   (dx_roll_prompts
      {do
         ($num {random_int 0 4096})
         ($num2 {random_int 0 100})
         {get_date_time $year $month $day $hour $minute $second}
         {if
            {&&
               {! $dx_msg_birth}
               ;{> $year 2023}
               {== $month 10}
               {== $day 25}
            }
            {set $dx_msg_birth TRUE}
            {dx_passive_messenger_symbol "Happy Birthday Shiloh!"}
         }
         ;if oct25th, congrats, no other rolls on this day
         {if {! $dx_msg_birth}
            {if {== $num 2112}
               {dx_passive_messenger you_lost_game}
            }
            ;roll to prompt for translations wanted screen
            {if {&& {!= $num 2112} {== $num2 42}}
               {ui_event_mgr trigger_event error_message
                  (dummy init localize_it)
               }
            }
         }
      }
   )
   (dx_correct_bns
      ;correct bns to new speed system
      {if {modifier_mgr is_modifier_active mod_doublespeed}
         {disable_modifier mod_doublespeed}
         {foreach $entry (guitar bass drum keys real_keys real_guitar real_bass)
            {set_var {sprint "trackspeed_" $entry}
               {dx_fixup_bns
                  {eval {var {sprint "trackspeed_" $entry}}}
               }
            }
         }
         {dx_settings_dta_writer}
      }
   )
   (dx_unlock_pre_orders
      ($user)
      {switch {$user get_slot_num}
         (0
            {main_hub_panel unlock_gamestop_award 0}
            {main_hub_panel unlock_x79_award 0}
            {main_hub_panel unlock_d2010_award 0}
         )
         (1
            {main_hub_panel unlock_gamestop_award 1}
            {main_hub_panel unlock_x79_award 1}
            {main_hub_panel unlock_d2010_award 1}
         )
         (2
            {main_hub_panel unlock_gamestop_award 2}
            {main_hub_panel unlock_x79_award 2}
            {main_hub_panel unlock_d2010_award 2}
         )
         (3
            {main_hub_panel unlock_gamestop_award 3}
            {main_hub_panel unlock_x79_award 3}
            {main_hub_panel unlock_d2010_award 3}
         )
      }
   )

   ;label functions
   (lbl_maker
      ($lbl $x $y $align $content)
      {new BandLabel $lbl}
      {$lbl set resource_name "pentatonic_outline"}
      {$lbl set alt_style_enabled TRUE}
      {$lbl set alt_font_resource_name pentatonic_outline}
      {if {! {exists now_play_color.color}}
         {new UIColor now_play_color.color}
         {now_play_color.color set color {pack_color 1 1 1}}
      }
      {$lbl set markup TRUE}
      {$lbl set_showing TRUE}
      {$lbl set_local_scale 1 1 1}
      {$lbl set_local_pos $x 0 $y}
      {$lbl set_token_fmt stringify {sprint $content}}
      {$lbl set text_size 15}
      {$lbl set alt_text_size 15}
      {$lbl set alt_z_offset -4}
      {$lbl set fit_type 2}
      {$lbl set alignment $align}
      {$lbl set width 300}
      {$lbl set height 40}
      {$lbl set alpha 1}
      ;{$label set color_override now_play_color.color}
      {main_menu.grp add_object $lbl}
   )
   (dx_buildver_lbl
      {do
         ($lbl dx_build.lbl)
         ;setup build label in case GoCentral is overriding our motd
         {if {! {exists $lbl}}
            {$this lbl_maker $lbl -377 216 kTopLeft os_blnk}
         }
         {$lbl set width 1000}
         {$lbl set_token_fmt rb3e_mod_string}
         #ifdef RB3E
         {do
            ($rb3e_allowed {rb3e_allowed})
            ($name {localize rb3e_name})
            ($string {localize rb3e_mod_string})
            ($tag
               {if_else $rb3e_allowed
                  {rb3e_build_tag}
                  {localize unknown}
               }
            )
            {$lbl set_token_fmt stringify
               {sprint $string "\n" $name " " $tag}
            }
         }
         #endif
      }
   )
   (dx_now_playing
      {main_menu.grp set_showing TRUE}
      {meta_performer reset_songs}
      {meta_performer set_song {symbol $metashortname}}
      {do
         ($lbl_0 line0.lbl)
         ($lbl_1 line1.lbl)
         ($lbl_2 line2.lbl)
         ($lbl_3 line3.lbl)
         ($anim now_playing.anim)
         {if {! {exists $lbl_0}}
            {$this lbl_maker $lbl_0 385 145 kTopRight {sprint $currentshellmusic}}
         }
         {if {! {exists $lbl_1}}
            {$this lbl_maker $lbl_1 385 160 kTopRight {sprint $currentshellmusic}}
            {$lbl_1 set alt_z_offset -2}
            {$lbl_1 set alt_text_size 12}
         }
         {if {! {exists $lbl_2}}
            {$this lbl_maker $lbl_2 385 145 kTopRight {sprint $currentshellmusic}}
         }
         {unless $dx_event_mode
            {if {! {exists $lbl_3}}
               {$this lbl_maker $lbl_3 385 145 kTopRight {sprint "<alt>j</alt>playnow"}}
               {$lbl_3 set alt_font_resource_name buttons}
               {$lbl_3 set alt_z_offset -1}
               {$lbl_3 set alt_text_size 12}
            }
         }
         {$lbl_0 set_token_fmt os_blnk}
         {$lbl_1 set_token_fmt stringify {eval $metasong}}
         {$lbl_2 set_token_fmt os_blnk}
         {unless $dx_event_mode
            {if_else {meta_performer song}
               {if {&& {session is_local} {meta_performer song_id}}
                  {$lbl_3 set_token_fmt press_to_play $metaverb}
               }
               {$lbl_3 set_token_fmt os_blnk}
            }
         }
         ;animate line1
         {if {exists $anim}
            {delete $anim}
         }
         {if {! {exists $anim}}
            {new PropAnim $anim}
            {$anim add_keys $lbl_1 (alt_alpha) kPropFloat}
            {$anim add_keys $lbl_1 (text_token) kPropSymbol}

            {do
               ($keys (0 1 1 1 1 0 1 1 1 1 0 0))
               {foreach_int $i 0 {size $keys}
                  {$anim set_key_val $lbl_1 (alt_alpha) $i {elem $keys $i}}
                  {$anim set_key_val $lbl_1 (text_token) $i {if_else {>= $i 5} {eval $metaartist} {eval $metasong}}}
               }
            }

            {$anim set rate kTaskUISeconds}
            {$anim set_frame 0}
            {$anim animate (loop 0 10) (period 10) (units kTaskUISeconds)}
         }
      }
   )
   (dx_metamusic_play
      {profile_mgr set_primary_profile_by_user $user}
      {meta_performer reset_songs}
      {meta_performer set_song {symbol $metashortname}}
      {gamemode set_mode qp_coop}
      {ui abstract_wipe}
      {if {exists line1_fade}
         {delete line1_fade}
      }
      {dx_restore_online_transitions}
      {ui goto_screen {music_library get_next_screen}}
   )

   ;play a show
   (dx_pas_reset_vars
      ;setup play a show menu button
      {mb_musicstore.btn set markup TRUE}
      {mb_musicstore.btn set alt_style_enabled TRUE}
      {mb_musicstore.btn set alt_font_resource_name {mb_musicstore.btn get resource_name}}
      {if $dx_entered_play_a_show ;removes "new" flavor text if played already
         {mb_musicstore.btn set text_token dx_play_a_show}
      }
      ;reset play a show vars in this enter branch (we arent in pas)
      {if {&& {! $dx_playing_a_show} {! $dx_prep_show_flow} {! $dx_in_show_flow}}
         {set $dx_play_a_show_active_instruments ()}
         {resize $dx_play_a_show_active_instruments 0}
      }
   )
   (dx_check_togo_play_a_show
      #ifdef HX_PS3
      {$this dx_go_to_play_a_show}
      #else ;xbox and wii rely on rb3e for play a show
         #ifndef RB3E
         {dx_modal_messenger rb3e_install_warn}
         #endif
         #ifdef RB3E
         {do
            ($pass FALSE)
            ($check {rb3e_get_song_name 1102})
            {if {&& {content_mgr refresh_done} {rb3e_allowed} {== {sprint $check} "Imagine"}}
               {set $pass TRUE}
            }
            {if {! {content_mgr refresh_done}}
               {dx_modal_messenger wait_for_songs}
            }
            {if $pass
               {$this dx_go_to_play_a_show}
            }
         }
         #endif
      #endif
   )
   (dx_go_to_play_a_show
      {input_mgr set_user $user}
      {set $stored_user $user}
      {set $dx_entered_play_a_show TRUE}
      {$this dx_play_a_show}
   )
   (dx_play_a_show ;TODO refactor when play a show is refactored because like, wtf
      {if {! $play_a_show_mentioned}
         {set $play_a_show_mentioned TRUE}
         {set $read_array ()}
         {set $song_array ()}
         {set $dtaarray ()}
         {resize $read_array 0}
         {resize $song_array 0}
         {resize $dtaarray 2}
         {set $num_voted_songs 0}
      }
      {do
         {set $dx_playing_a_show TRUE}
         {dx_presence_manager}
         {set $current_voted_song FALSE}
         {set $dx_playlist_array ()}
         {resize $dx_playlist_array 0}
         {set $dx_playlist_queue_array ()}
         {resize $dx_playlist_queue_array 0}
         {set $dx_play_a_show_required_votes 0}
         {set $dx_play_a_show_num_votes 0}
         {set $slot1_voted FALSE}
         {set $slot2_voted FALSE}
         {set $slot3_voted FALSE}
         {set $slot4_voted FALSE}
         {set $slot1_vote FALSE}
         {set $slot2_vote FALSE}
         {set $slot3_vote FALSE}
         {set $slot4_vote FALSE}
         {gamemode set_mode qp_coop}
         {if_else {== {size $dx_play_a_show_active_instruments} 0}
            {do
               {set $rb3_songlist
                  (
                     20thcenturyboy 25or6to4 antibodies
                     beforeiforget bohemianrhapsody breakonthrough centerfold
                     chinagrove coldasice combatbaby crosstowntraffic deadendfriends
                     dontstandsoclosetome duhast everybodywantstorule flylikeaneagle
                     foolin getupstandup goodvibrationslive heartofglass hereigoagain
                     humanoid ifeelgoodalt iloverocknroll inabigcountry
                     ineedtoknow inthemeantime justlikeheaven killingloneliness
                     lastdance livinginamerica lowrider meenamora midlifecrisis
                     needyoutonight ohmygod
                     oyemiamor portionsforfoxes radarlove rainbowinthedark rehab rocklobster roundabout
                     saturdaynightsalright sisterchristian smokeonthewater somethingbigger spaceoddity
                     stopmeifyouthink thebeautifulpeople thecon thekillingmoon thelook
                     thepoweroflove thisbastardslife walkinonthesun walkoflife
                     werewolvesoflondon whipit yoshimibattles
                  )
               }
               {meta_performer set_song {random_elem $rb3_songlist}}
               {set $dx_prep_show_flow TRUE}
               {ui abstract_wipe}
               #ifndef HX_WII
               {ui goto_screen dx_playashow_intro_screen}
               #else
               {ui goto_screen song_select_enter_screen}
               #endif
            }
            {do
               {if_else $dx_hardware
                  {dx_passive_messenger play_a_show_loading_hw}
                  {dx_passive_messenger play_a_show_loading_emu}
               }
               {set $dx_in_show_flow TRUE}
               {meta_performer set_song antibodies}
               {if {is_leader_local}
                  ;{session send_msg_to_all {'`' (set $dx_in_show_flow TRUE) kNetReliable}}
                  ;{session send_msg_to_all {'`' (meta_performer set_song antibodies) kNetReliable}}
                  {ui sync_screen dx_play_a_show_screen 0}
               }
            }
         }
      }
   )
)
{new MainHubPanel
   main_hub_panel
   (file "main_hub.milo")
   (focus "mb_playnow.btn")
   (first_user "")
   (last_focused_button "mb_playnow.btn")
   (message_rotation_ms 5000)
   (band_provider 0)
   (message_area_expanded FALSE)
   (waiting_for_store_entry FALSE)
   (waiting_for_token_entry FALSE)
   (waiting_for_leaderboard_entry FALSE)
   (update_messages
      ($user $messages_received)
      {set $messages ()}
      {resize $messages 0}
      {push_back $messages motd}
      {if
         {&&
            $messages_received
            {$this check_profile_for_message_ticker}
            #ifndef HX_WII
            {!= {$this get_dlcmotd} ""}
            #endif
         }
         {push_back $messages dlc_motd}
      }
      {if {&& $messages_received {$this has_unlinked_motd}}
         {push_back $messages unlinked_motd}
      }
      {if $user
         {do
            ($current_level $next_level)
            {set $current_level {campaign get_campaign_level $user}}
            {if {! {campaign is_last_campaign_level $current_level}}
               {set $next_level {campaign get_next_campaign_level $current_level}}
               {push_back
                  $messages
                  {campaign get_campaign_level_advertisement $next_level}
               }
            }
         }
      }
      {if {&& $messages_received {$this check_profile_for_message_ticker}}
         {if {$this has_role_info}
            {push_back $messages leaderboard_summary}
         }
         {if {$this has_band_info}
            {push_back $messages leaderboard_band_summary}
         }
         {if {$this has_battles_info}
            {push_back $messages battles_summary}
         }
      }
      $messages
   )
   (motd {$this get_motd})
   (dlc_motd {$this get_dlcmotd})
   (is_loaded {saveload_mgr is_initial_load_done})
   (enter
      {if [first_user]
         {profile_mgr set_primary_profile_by_user [first_user]}
         {$this set first_user ""}
      }
      #ifdef HX_WII
      {if {platform_mgr is_online_restricted}
         {do
            {qp_fmp.btn set_state 2}
            {tour_fmp.btn set_state 2}
            {career_leaderboard.btn set_state 2}
            {redeem_token.btn set_state 2}
         }
      }
      {unless {session_mgr is_local}
         {mb_shop.btn set_state 2}
      }
      {session add_sink $this (new_remote_user)}
      {session add_sink $this (remote_user_left)}
      #endif
      {unless {ui_event_mgr has_transition_event go_to_charactercreator}
         {input_mgr clear_user}
         {closet_mgr clear_user}
         {critical_user_listener clear_critical_user}
      }
      {gamemode set_mode init}
      {net_sync set_ui_state kNetUI_MainMenu}
      {set [waiting_for_store_entry] FALSE}
      {set [waiting_for_token_entry] FALSE}
      {set [waiting_for_leaderboard_entry] FALSE}
      {platform_mgr add_sink $this}
      {input_mgr set_auto_vocals_confirm_allowed}
      {input_mgr check_trigger_auto_vocals_confirm}
      {overshell set_allow_real_guitar_flow TRUE}
      {overshell update_all}
      #ifndef HX_PS3
      {content_mgr start_refresh}
      #endif
      {campaign clear_current_goal}
      {$this set_state kMainHubState_Main}
      ;end vanilla setup

      ;everything else on enter should be done in dx_main_on_enter handler
      ;via calling other handlers
      {$this dx_pas_reset_vars} ;this always needs to happen
      {if_else {&& $dx_playing_a_show {is_leader_local}}
         {$this dx_play_a_show}
         {$this dx_main_on_enter}
      }
      {set $got_meta FALSE} ;setup now playing via poll
   )
   DX_MAIN_HANDLES ;hook all of our added handles
   #ifndef HX_WII
   (poll
      {if {&& {! $got_meta} {content_mgr refresh_done}}
         {set $got_meta TRUE}
         {$this dx_now_playing}
      }
   )
   #endif
   (exit
      {platform_mgr remove_sink $this}
      #ifdef HX_WII
      {session remove_sink $this new_remote_user}
      {session remove_sink $this remote_user_left}
      #endif
   )
   (SELECT_MSG
      {if_else $dx_event_mode
         {switch $component
            (mb_playnow.btn
               {profile_mgr set_primary_profile_by_user $user}
               {net_sync set_ui_state kNetUI_WaitingChooseSong}
               {$this set_override kMainHubOverride_Waiting}
            )
            (mb_career.btn
               {input_mgr set_user $user}
               {set $stored_user $user}
               {$this dx_play_a_show}
            )
            kDataUnhandled
         }
         {switch $component
            (mb_playnow.btn {$this set_state kMainHubState_PlayNow})
            (mb_career.btn {$this set_state kMainHubState_Career})
            (mb_trainers.btn
               {if_else {session_mgr is_local}
                  {$this set_state kMainHubState_Training}
                  {push_basic_confirm_dialog no_trainers_when_online}
               }
            )
            (mb_shop.btn {$this set_state kMainHubState_Customize})
            (customize_band.btn
               {if_else {profile_mgr has_primary_profile}
                  {do
                     {profile_mgr set_primary_profile_by_user $user}
                     {manage_band_panel set_profile {profile_mgr get_primary_profile}}
                     {manage_band_screen set back_override main_hub_screen}
                     {ui goto_screen manage_band_screen}
                  }
                  {push_signin_needed_dialog manage_band_no_profile}
               }
            )
            (customize_character.btn
               {if_else TRUE ;lol customize all characters ; vanilla - {$user is_char_customizable}
                  {do
                     {critical_user_listener set_critical_user $user}
                     ;unlock pre-order bonus's before going to character edit
                     {if {profile_mgr has_primary_profile}
                        {dx_unlock_pre_orders $user}
                     }
                     {closet_mgr set_user $user}
                     {ui goto_screen customize_clothing_enter_screen}
                  }
                  {do
                     ($profile {profile_mgr get_profile $user})
                     {if_else {&& $profile {== {$profile get_num_chars} 0} {session_mgr is_local}}
                        {do
                           {input_mgr set_user $user}
                           {critical_user_listener set_critical_user $user}
                           {ui push_screen customize_create_character_screen}
                        }
                        {ui push_screen customize_no_prefabs_screen}
                     }
                  }
               }
            )
            (create_character.btn
               {if_else {! {profile_mgr get_profile $user}}
                  {push_signin_needed_dialog create_character_must_signin}
                  {do
                     {if_else {session_mgr is_local}
                        {do
                           ($profile {profile_mgr get_profile $user})
                           {if_else {< {$profile get_num_chars} {$profile max_chars}}
                              {do
                                 {critical_user_listener set_critical_user $user}
                                 {closet_mgr set_user $user}
                                 {closet_mgr set_return_screen main_hub_screen}
                                 {net_sync disable}
                                 {ui goto_screen charactercreator_screen}
                                 #ifdef HX_WII
                                 {overshell set_active_status kOvershellInactive}
                                 #endif
                              }
                              {ui push_screen create_character_maxed_screen}
                           }
                        }
                        {ui push_screen create_character_notonline_screen}
                     }
                  }
               }
            )
            (mb_musicstore.btn {$this dx_check_togo_play_a_show})
            (pn_quickplay.btn {$this set_state kMainHubState_Quickplay})
            (pn_tour.btn {$this set_state kMainHubState_Tour})
            (qp_quickplay.btn
               {profile_mgr set_primary_profile_by_user $user}
               {net_sync set_ui_state kNetUI_WaitingChooseSong}
               {$this set_override kMainHubOverride_Waiting}
            )
            (qp_setlist.btn
               {profile_mgr set_primary_profile_by_user $user}
               {net_sync set_ui_state kNetUI_WaitingChooseSetlist}
               {$this set_override kMainHubOverride_Waiting}
            )
            (qp_fmp.btn
               {if_else {overshell is_full}
                  {push_basic_confirm_dialog no_fmp_when_full}
                  {do
                     {profile_mgr set_primary_profile_by_user $user}
                     {net_sync set_ui_state kNetUI_WaitingQpFindPlayers}
                     {$this set_override kMainHubOverride_Waiting}
                  }
               }
            )
            (tour_new.btn
               {profile_mgr set_primary_profile_by_user $user}
               {$this handle_tournew_chosen}
            )
            (tour_fmp.btn
               {if_else {overshell is_full}
                  {push_basic_confirm_dialog no_fmp_when_full}
                  {do
                     {profile_mgr set_primary_profile_by_user $user}
                     {$this handle_tourfmp_chosen}
                  }
               }
            )
            (career_progress.btn
               {profile_mgr set_primary_profile_by_user $user}
               {$this handle_campaign_chosen}
            )
            (career_songs.btn
               {profile_mgr set_primary_profile_by_user $user}
               {$this handle_campaign_songs_chosen}
            )
            (career_leaderboard.btn
               {profile_mgr set_primary_profile_by_user $user}
               {$this handle_campaign_lb_chosen}
            )
            (training_band_practice.btn
               {if_else {session_mgr is_local}
                  {do
                     {gamemode set_mode practice}
                     {ui abstract_wipe}
                     {input_mgr set_user $user}
                     {critical_user_listener set_critical_user $user}
                     {training_mgr set_return_info song_select_screen pause_quit}
                     {training_mgr set_user $user}
                     {ui goto_screen song_select_enter_screen}
                  }
                  {push_basic_confirm_dialog no_trainers_when_online}
               }
            )
            (training_trainers.btn
               {cond
                  ({!
                        {session_mgr is_local}}
                     {push_basic_confirm_dialog no_trainers_when_online}
                  )
                  ({!
                        {profile_mgr has_primary_profile}}
                     {push_signin_needed_dialog trainer_must_signin}
                  )
                  {gamemode set_mode trainer}
                  {profile_mgr set_primary_profile_by_user $user}
                  {ui goto_screen training_screen}
               }
            )
            (training_pro_song_lessons.btn
               {cond
                  ({!
                        {session_mgr is_local}}
                     {push_basic_confirm_dialog no_trainers_when_online}
                  )
                  ({!
                        {profile_mgr get_profile $user}}
                     {push_signin_needed_dialog trainer_must_signin}
                  )
                  ({==
                        {$user get_controller_type}
                        kControllerKeys}
                     {gamemode set_mode pro_song_lessons_keyboard}
                     {input_mgr set_user $user}
                     {critical_user_listener set_critical_user $user}
                     {training_mgr set_user $user}
                     {training_mgr set_minimum_difficulty kDifficultyEasy}
                     {training_mgr set_return_info song_select_screen pause_quit}
                     {song_select_panel set trainer_from_main_menu kControllerKeys}
                     {ui abstract_wipe}
                     {ui goto_screen song_select_enter_screen}
                  )
                  ({||
                        {== {$user get_controller_type} kControllerRealGuitar}
                        ; dx - allow 5L guitar to enter Pro Guitar trainer for playtesting
                        {&&
                           {== {$user get_controller_type} kControllerGuitar}
                           {modifier_mgr is_modifier_active mod_auto_play}
                        }
                     }
                     {gamemode set_mode trainer_real_guitar}
                     {input_mgr set_user $user}
                     {critical_user_listener set_critical_user $user}
                     {training_mgr set_user $user}
                     {training_mgr set_minimum_difficulty kDifficultyEasy}
                     {training_mgr set_return_info song_select_screen pause_quit}
                     {song_select_panel set trainer_from_main_menu kControllerRealGuitar}
                     {ui abstract_wipe}
                     {ui goto_screen song_select_enter_screen}
                  )
                  {push_basic_confirm_dialog pro_song_lessons_wrong_controller}
               }
            )
            (training_drum_freestyle.btn
               {cond
                  ({!
                        {session_mgr is_local}}
                     {push_basic_confirm_dialog no_trainers_when_online}
                  )
                  ({==
                        {$user get_controller_type}
                        kControllerDrum}
                     {gamemode set_mode drum_freestyle}
                     {input_mgr set_user $user}
                     {critical_user_listener set_critical_user $user}
                     {training_mgr set_user $user}
                     {training_mgr set_return_info main_hub_screen overshell_quit_freestyle}
                     {ui goto_screen drum_freestyle_screen}
                  )
                  {push_basic_confirm_dialog drum_freestyle_wrong_controller}
               }
            )
            (
               (dlc_store.btn redeem_token.btn wii_manageAOC.btn)
               {store_real_loading_screen set
                  store_mode
                  {switch $component
                     (dlc_store.btn dlc)
                     (redeem_token.btn token)
                     (wii_manageAOC.btn manage)
                  }
               }
               #ifndef HX_WII
               {input_mgr set_user $user}
               #endif
               #ifdef HX_XBOX
               {if_else
                  {'||'
                     {! {platform_mgr is_user_signed_into_live $user}}
                     {platform_mgr is_user_a_guest $user}
                  }
                  {ui push_screen main_store_signin_screen}
                  {do
                     {ui abstract_wipe}
                     {ui goto_screen store_loading_screen}
                  }
               }
               #endif
               #ifdef HX_PS3
               {if_else {== $component dlc_store.btn}
                  {set [waiting_for_store_entry] TRUE}
                  {set [waiting_for_token_entry] TRUE}
               }
               {platform_mgr run_net_start_utility}
               #endif
               #ifdef HX_PC
               {ui goto_screen store_loading_screen}
               #endif
               #ifdef HX_WII
               {$this set_state kMainHubState_Main}
               {ui goto_screen store_wii_enter_screen}
               #endif
            )
         }
      }
   )
   (handle_goto_tour
      {net_sync set_ui_state kNetUI_WaitingTour}
      {$this set_override kMainHubOverride_Waiting}
   )
   (handle_goto_tourfmp
      {net_sync set_ui_state kNetUI_WaitingTourFindPlayers}
      {$this set_override kMainHubOverride_Waiting}
   )
   (handle_tournew_chosen {$this handle_goto_tour})
   (handle_tourfmp_chosen
      {if_else {profile_mgr has_primary_profile}
         {$this handle_goto_tourfmp}
         {push_signin_needed_dialog tour_need_signed_in_user}
      }
   )
   (handle_goto_campaign
      {gamemode set_mode campaign}
      {accomplishment_panel clear_career_state}
      {ui goto_screen accomplishment_screen}
   )
   (handle_goto_campaign_songs
      {gamemode set_mode qp_career_songinfo}
      {ui goto_screen accomplishment_songs_screen}
   )
   (handle_goto_campaign_lb
      #ifdef HX_PS3
      {set [waiting_for_leaderboard_entry] TRUE}
      {platform_mgr run_net_start_utility}
      #else
      {gamemode set_mode campaign}
      {campaign_leaderboard_hub_panel set return_screen main_hub_screen}
      {ui goto_screen campaign_leaderboard_hub_screen}
      #endif
   )
   (handle_campaign_chosen
      {if_else {profile_mgr has_primary_profile}
         {$this handle_goto_campaign}
         {push_signin_needed_dialog campaign_need_signed_in_user}
      }
   )
   (handle_campaign_songs_chosen
      {if_else {profile_mgr has_primary_profile}
         {$this handle_goto_campaign_songs}
         {push_signin_needed_dialog campaign_need_signed_in_user}
      }
   )
   (handle_campaign_lb_chosen
      {if_else {profile_mgr has_primary_profile}
         {$this handle_goto_campaign_lb}
         {push_signin_needed_dialog campaign_need_signed_in_user}
      }
   )
   (advance
      ($waiting_state)
      {switch $waiting_state
         (kNetUI_WaitingPartyShuffle
            {gamemode set_mode party_shuffle}
            {ui sync_screen part_difficulty_screen 0}
         )
         (kNetUI_WaitingChooseSong
            {gamemode set_mode qp_coop}
            {ui abstract_wipe}
            {ui sync_screen song_select_enter_screen 0}
         )
         (kNetUI_WaitingChooseSetlist
            {gamemode set_mode qp_coop}
            {music_library start_in_setlist_browser}
            {ui abstract_wipe}
            {ui sync_screen song_select_enter_screen 0}
         )
         (kNetUI_WaitingTour
            {if_else {profile_mgr has_primary_profile}
               {do
                  {gamemode set_mode tour}
                  {if {session_mgr is_leader_local}
                     {ui sync_screen tour_customize_screen 0}
                  }
               }
               {do
                  {net_sync set_ui_state kNetUI_MainMenu}
                  {$this set_override kMainHubOverride_None}
                  {push_signin_needed_dialog tour_need_signed_in_user}
               }
            }
         )
         (kNetUI_WaitingQpFindPlayers
            {net_sync set_ui_state kNetUI_FindPlayersQp}
            {$this set_state kMainHubState_Quickplay}
            {$this set_override kMainHubOverride_Finding}
         )
         (kNetUI_WaitingTourFindPlayers
            {net_sync set_ui_state kNetUI_FindPlayersTour}
            {$this set_state kMainHubState_Tour}
            {$this set_override kMainHubOverride_Finding}
         )
      }
   )
   (cancel_find_override
      {unless {session_mgr is_local}
         {session_mgr disconnect}
      }
      {net_sync set_ui_state kNetUI_MainMenu}
      {$this set_override kMainHubOverride_None}
   )
   (cancel_waiting_override
      {net_sync set_ui_state kNetUI_MainMenu}
      {$this set_override kMainHubOverride_None}
   )
   (BUTTON_DOWN_MSG
      {cond
         ({$this in_waiting_lock} TRUE)
         ({== $action kAction_Cancel}
            {cond
               ({==
                     {$this get_override}
                     kMainHubOverride_Waiting}
                  {$this cancel_waiting_override}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_override}
                     kMainHubOverride_Finding}
                  {if_else {session_mgr is_local}
                     {$this cancel_find_override}
                     {ui push_screen disconnect_find_players_confirm_screen}
                  }
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_PlayNow}
                  {$this set_focus mb_playnow.btn}
                  {$this set_state kMainHubState_Main}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Quickplay}
                  {$this set_focus pn_quickplay.btn}
                  {$this set_state kMainHubState_PlayNow}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Tour}
                  {$this set_focus pn_tour.btn}
                  {$this set_state kMainHubState_PlayNow}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Career}
                  {$this set_focus mb_career.btn}
                  {$this set_state kMainHubState_Main}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Training}
                  {$this set_focus mb_trainers.btn}
                  {$this set_state kMainHubState_Main}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Customize}
                  {$this set_focus mb_shop.btn}
                  {$this set_state kMainHubState_Main}
                  {play_instr_sfx $user button_back}
               )
               ({==
                     {$this get_state}
                     kMainHubState_Store}
                  {$this set_focus mb_musicstore.btn}
                  {$this set_state kMainHubState_Main}
                  {play_instr_sfx $user button_back}
               )
               (TRUE kDataUnhandled)
            }
         )
         ({== {$this get_override} kMainHubOverride_Waiting} TRUE)
         ({==
               {$this get_override}
               kMainHubOverride_Finding}
            {if {&& {== $action kAction_Confirm} {session_mgr is_leader_local}}
               {if_else {machine_mgr all_machines_have_same_net_ui_state}
                  {$this advance_from_finding}
                  {ui_event_mgr trigger_event remote_not_ready}
               }
            }
         )
         ({&&
               {== $action kAction_Option}}
            {$this cycle_next_message}
         )
         ({== $action kAction_ViewModify}
            {profile_mgr set_primary_profile_by_user $user}
            {$this check_rewards_and_hints}
         )
         #ifndef HX_WII
         ;play metamusic shortcut
         ({|| {== $action kAction_PageUp} {== $action kAction_PageDown}}
            {if_else ;we need to check in stages or it will crash
               {&&
                  {content_mgr refresh_done}
                  {! {ui in_transition}}
               }
               {if_else {meta_performer song}
                  {if_else {&& {session is_local} {meta_performer song_id}}
                     {$this dx_metamusic_play}
                     kDataUnhandled
                  }
                  kDataUnhandled
               }
               kDataUnhandled
            }
         )
         #endif
         kDataUnhandled
      }
   )
   (refresh_message_provider
      {message_area.lst set_provider {$this get_message_provider}}
   )
   (update_message_counter
      ($current $max)
      {expand_message_area.ihp set_action_token
         kAction_Option
         (main_hub_next_message $current $max)
      }
   )
   (update_state_view
      ($state $old_state $override $old_override)
      {switch {$this get_override}
         (kMainHubOverride_None {override_none.trg trigger})
         (kMainHubOverride_Waiting {override_waiting.trg trigger})
         (kMainHubOverride_Finding
            {override_finding.trg trigger}
            {$this update_finding_help}
         )
      }
      {cond
         ({== $state $old_state} TRUE)
         ({== $state kMainHubState_Main}
            {if {== $old_state kMainHubState_None}
               {$this set_focus mb_playnow.btn}
            }
            {switch $old_state
               (kMainHubState_None {none_to_main.trg trigger})
               (kMainHubState_PlayNow {playnow_to_main.trg trigger})
               (kMainHubState_Career {career_to_main.trg trigger})
               (kMainHubState_Training {training_to_main.trg trigger})
               (kMainHubState_Customize {customize_to_main.trg trigger})
               (kMainHubState_Store {store_to_main.trg trigger})
            }
         )
         ({== $state kMainHubState_PlayNow}
            {if {== $old_state kMainHubState_Main}
               {$this set_focus pn_quickplay.btn}
            }
            {switch $old_state
               (kMainHubState_Main {main_to_playnow.trg trigger})
               (kMainHubState_Quickplay {quickplay_to_playnow.trg trigger})
               (kMainHubState_Tour {tour_to_playnow.trg trigger})
            }
         )
         ({== $state kMainHubState_Quickplay}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus qp_quickplay.btn}
            }
            {if {!= $state $old_state}
               {playnow_to_quickplay.trg trigger}
            }
         )
         ({== $state kMainHubState_Tour}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus tour_new.btn}
            }
            {if {!= $state $old_state}
               {playnow_to_tour.trg trigger}
            }
         )
         ({== $state kMainHubState_Training}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus training_band_practice.btn}
            }
            {if {!= $state $old_state}
               {main_to_training.trg trigger}
            }
         )
         ({== $state kMainHubState_Career}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus career_progress.btn}
            }
            {if {!= $state $old_state}
               {main_to_career.trg trigger}
            }
         )
         ({== $state kMainHubState_Customize}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus customize_band.btn}
            }
            {if {!= $state $old_state}
               {main_to_customize.trg trigger}
            }
         )
         ({== $state kMainHubState_Store}
            {if {!= $old_state kMainHubState_None}
               {$this set_focus dlc_store.btn}
            }
            {if {!= $state $old_state}
               {main_to_store.trg trigger}
            }
         )
      }
      {if {&& {!= [last_focused_button] ""} {== $old_state kMainHubState_None}}
         {$this set_focus [last_focused_button]}
      }
   )
   (FOCUS_MSG
      {if {== $panel_dir main_hub}
         {$this set last_focused_button $new_focus}
         ;show detailed description based on focus
         {if $dx_event_mode
            {do
               ($lbl event_button_desc.lbl)
               {switch $new_focus
                  (mb_playnow.btn {$lbl set_token_fmt dx_freeplay_desc})
                  (mb_career.btn {$lbl set_token_fmt dx_play_a_show_desc})
                  {$lbl set_token_fmt os_blnk}
               }
            }
         }
      }
   )
   (update_pool_info
      ($has_stats $rating0 $rating1 $rating2 $rating3)
      {foreach $pair
         (
            (pool_info0 $rating0)
            (pool_info1 $rating1)
            (pool_info2 $rating2)
            (pool_info3 $rating3)
         )
         {do
            ($widget {elem $pair 0})
            ($rating {eval {elem $pair 1}})
            {with $widget
               {cond
                  ({! $has_stats}
                     {unknown.trg trigger}
                  )
                  ({== $rating 0}
                     {red.trg trigger}
                  )
                  ({== $rating 1}
                     {yellow.trg trigger}
                  )
                  ({== $rating 2}
                     {green.trg trigger}
                  )
               }
            }
         }
      }
   )
   (net_start_utility_finished
      ($success)
      {cond
         #ifndef HX_WII
         ([waiting_for_store_entry]
            {if $success
               {ui goto_screen store_loading_screen}
            }
            {set [waiting_for_store_entry] FALSE}
         )
         ([waiting_for_token_entry]
            {if $success
               {ui goto_screen token_redemption_screen}
            }
            {set [waiting_for_token_entry] FALSE}
         )
         #endif
         ([waiting_for_leaderboard_entry]
            {if $success
               {gamemode set_mode campaign}
               {campaign_leaderboard_hub_panel set return_screen main_hub_screen}
               {ui goto_screen campaign_leaderboard_hub_screen}
            }
            {set [waiting_for_leaderboard_entry] FALSE}
         )
      }
   )
   (unlock_gamestop_award
      ($pad_num)
      {if {! {ui in_transition}}
         {do
            ($user {user_mgr get_user_from_pad_num $pad_num})
            ($profile {profile_mgr get_profile $user})
            {if $profile
               {{$profile access_accomplishment_progress} add_award
                  award_gamestop
                  campaign_award_reason_preorder
               }
               {$this check_rewards_and_hints}
            }
         }
      }
   )
   (unlock_x79_award
      ($pad_num)
      {if {! {ui in_transition}}
         {do
            ($user {user_mgr get_user_from_pad_num $pad_num})
            ($profile {profile_mgr get_profile $user})
            {if $profile
               {{$profile access_accomplishment_progress} add_award
                  award_bonus_x79
                  campaign_award_reason_preorder
               }
               {$this check_rewards_and_hints}
            }
         }
      }
   )
   (unlock_d2010_award
      ($pad_num)
      {if {! {ui in_transition}}
         {do
            ($user {user_mgr get_user_from_pad_num $pad_num})
            ($profile {profile_mgr get_profile $user})
            {if $profile
               {{$profile access_accomplishment_progress} add_award
                  award_bonus_d2010
                  campaign_award_reason_preorder
               }
               {$this check_rewards_and_hints}
            }
         }
      }
   )
   (check_rewards_and_hints
      {if {session_mgr is_local}
         {if_else
            {'||'
               {acc_mgr has_new_reward_vignettes}
               {acc_mgr has_new_awards}
               {campaign has_hints_to_show}
               {meta_performer are_credits_pending}
            }
            {ui goto_screen meta_loading_main_screen}
            {show_hint hint_rb3_welcome_screen}
         }
      }
   )
   (TRANSITION_COMPLETE_MSG
      {$this dx_check_first_time}
      DX_AUTOSAVE
   )
   (update_finding_help
      {finding_continue.ihp set_showing {session_mgr is_leader_local}}
      {do
         ($leader_user {session_mgr get_leader_user})
         ($show_waiting {&& $leader_user {! {$leader_user is_local}}})
         {waiting_on_leader.lbl set_showing $show_waiting}
         {if $show_waiting
            {waiting_on_leader.lbl set_token_fmt
               main_menu_waiting_on_leader
               {$leader_user get_player_name}
            }
         }
      }
   )
   #ifdef HX_WII
   (new_remote_user
      {if {== {$this get_state} kMainHubState_Customize}
         {do
            {$this set_focus mb_playnow.btn}
            {$this set_state kMainHubState_Main}
            {play_instr_sfx $user button_back}
         }
      }
      {unless {session_mgr is_local}
         {mb_shop.btn set_state 2}
      }
   )
   (remote_user_left
      {if {session_mgr is_local}
         {do
            {mb_shop.btn set_state 0}
         }
      }
   )
   #endif
}
{new BandScreen
   main_hub_screen
   (panels
      meta
      #ifdef HX_WII
      sv3_panel
      #else
      {if_else {file_exists DX_MENU_BACKGROUND_BIK_PATH}
         song_movie_panel
         sv3_panel
      }
      #endif
      main_hub_panel
      accomplishments_status_panel
   )
   (enter
      {accomplishments_status_panel set_showing $overshell_showing} ; dx - hide ui elements if in screensaver
      {main_hub_panel set_showing $overshell_showing}
   )
   (focus main_hub_panel)
}
{new BandScreen
   song_select_enter_screen
   (panels meta sv4_panel)
   (block_wipe_in TRUE)
   (enter {sv4_panel set_showing FALSE})
   (TRANSITION_COMPLETE_MSG
      {ui abstract_wipe_in}
      {ui goto_screen song_select_screen}
   )
}
{new BandScreen
   customize_clothing_enter_screen
   (panels meta customize_clothing_panel)
   (enter
      {customize_clothing_panel set_showing FALSE}
      {customize_panel set_showing FALSE}
   )
   (TRANSITION_COMPLETE_MSG {ui goto_screen customize_clothing_screen})
}
{new BandScreen
   create_character_maxed_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok accomplishment_cant_launch_character_creator})
   (SELECT_MSG {ui pop_screen main_hub_screen})
}
{new BandScreen
   create_character_notonline_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok overshell_char_creator_denial_remote_players})
   (SELECT_MSG {ui pop_screen main_hub_screen})
}
{new BandScreen
   customize_no_prefabs_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok customize_no_prefabs})
   (SELECT_MSG {ui pop_screen main_hub_screen})
}
{new BandScreen
   disconnect_find_players_confirm_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (former_net_ui_state 0)
   (enter
      #ifdef HX_WII
      {set [former_net_ui_state] {net_sync get_ui_state}}
      {net_sync set_ui_state kNetUI_InDialog}
      #endif
      {dialog_panel set_yesno disconnect_find_players_confirm no.btn}
   )
   (SELECT_MSG
      {if {== $component yes.btn}
         {main_hub_screen cancel_find_override}
      }
      #ifdef HX_WII
      {net_sync set_ui_state [former_net_ui_state]}
      #endif
      {ui pop_screen main_hub_screen}
   )
}
{new BandScreen
   customize_create_character_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter
      {platform_mgr add_sink $this (signin_changed)}
      {session add_sink $this (local_user_left)}
      {session add_sink $this (new_remote_user)}
      {dialog_panel set_custom
         customize_create_character
         customize_create_character_now
         customize_create_character_later
         opt2.btn
      }
   )
   (exit
      {platform_mgr remove_sink $this signin_changed}
      {session remove_sink $this local_user_left}
      {session remove_sink $this new_remote_user}
   )
   (new_remote_user {ui pop_screen main_hub_screen})
   (local_user_left {ui pop_screen main_hub_screen})
   (signin_changed {ui pop_screen main_hub_screen})
   (SELECT_MSG
      {switch $component
         (opt1.btn
            {closet_mgr set_user $user}
            {closet_mgr set_return_screen main_hub_screen}
            {net_sync disable}
            #ifdef HX_WII
            {overshell set_active_status kOvershellInactive}
            #endif
            {ui pop_screen charactercreator_screen}
         )
         (opt2.btn
            {input_mgr clear_user}
            {critical_user_listener clear_critical_user}
            {ui pop_screen main_hub_screen}
         )
      }
   )
}
{new BandScreen
   dialog_need_signin_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (signin_msg '')
   (enter
      {dialog_panel set_custom
         [signin_msg]
         mm_need_a_profile_signin
         cancel
         opt1.btn
      }
      {platform_mgr add_sink $this (signin_changed)}
   )
   (exit {platform_mgr remove_sink $this signin_changed})
   (signin_changed {ui pop_screen})
   (SELECT_MSG
      {cond
         ({== $component opt1.btn}
            #ifdef HX_WII
            {platform_mgr signin 1 FALSE $user}
            #else
            {platform_mgr signin 1 FALSE}
            #endif
            {ui pop_screen}
         )
         ({== $component opt2.btn}
            {ui pop_screen}
         )
      }
   )
}
{func push_signin_needed_dialog
   ($dialog_text)
   {dialog_need_signin_screen set signin_msg $dialog_text}
   {ui push_screen dialog_need_signin_screen}
}