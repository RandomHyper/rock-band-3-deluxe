#include song_select_extras.dta
{new SongSelectPanel
   song_select_panel
   (file "song_select.milo")
   (focus song.lst)
   (get_page_up_button
      {if_else {== {$user get_controller_type} kControllerVocals}
         kAction_ShellOption
         kAction_PageUp
      }
   )
   (get_page_down_button
      {if_else {== {$user get_controller_type} kControllerVocals}
         kAction_ShellOption
         kAction_PageDown
      }
   )
   (held_buttons
      (kAction_PageUp 0.25)
      (kAction_PageDown 0.25)
      (kAction_ShellOption 0.25)
      (kAction_Confirm 1.5)
      (kAction_Cancel 1.5)
   )
   (joypad
      (hold_ms 100)
      (repeat_ms 80)
   )
   (song_preview_delay 0.25)
   (mini_leaderboard_rotation_off 8.0)
   (mini_leaderboard_rotation_on 3.0)
   (details_mode FALSE)
   (lb_list "")
   (resume_setlist_edit FALSE)
   (should_go_to_art_maker FALSE)
   (reviews_dirty FALSE)
   (trainer_from_main_menu kControllerNone)
   (song_data_mounted {$this refresh_top})
   (get_option_button kAction_Option)
   (get_button_for_review
      {if_else {== {$user get_controller_type} kControllerVocals}
         kAction_Right
         kAction_Option
      }
   )
   ; Called by the search panel to hide some stuff to make way for search elements
   (activate_search
      {if_else {== {music_library get_current_sort_name} by_song}
         {music_library reset_filters}
         {{music_library get view_settings_provider} reset_all_settings}
      }
      {song.lst set_showing FALSE}
      {song.sbd set_showing FALSE}
      {help.ihp set_showing FALSE}
      {settings.ihp set_showing FALSE}
      {viewing.ihp set_showing FALSE}
      {music_library.lbl set_token_fmt store_search} ; TODO: use a special localization token?
      {status.lbl set_showing FALSE}
      {stats.grp set_showing FALSE}
   )
   (deactivate_search
      {song.lst set_showing TRUE}
      {song.sbd set_showing TRUE}
      {help.ihp set_showing TRUE}
      {settings.ihp set_showing TRUE}
      {viewing.ihp set_showing TRUE}
      {music_library.lbl set_token_fmt music_library}
      {status.lbl set_showing TRUE}
      {stats.grp set_showing TRUE}
   )
   (keyboard_msg ($key $shift $ctrl $alt)
      ; If the user types anything, open the search screen and forward the input
      {dx_log_writer insane "Keyboard input"}
      {ui push_screen dx_search_screen}
      {dx_search_panel keyboard_msg $key $shift $ctrl $alt}
   )
   (poll
      {if $probeforfinder
         {if
            {||
               $dx_in_show_flow
               #ifdef RB3E
               TRUE
               #else
               {! $dx_more_info_toggle}
               #endif
            }
            {$this dx_more_info_load TRUE}
         }
      }
   )
   (dx_user_sort
      {if {&& {!= $dx_song_select_sort_setting_1 none} {gamemode in_mode qp_coop}}
         {if {! $stored_sorted}
            ;sort via the stored setting, but only once
            {set $stored_sorted TRUE}
            {{music_library get view_settings_provider} refresh_all_settings}
            {song_select_filter_panel filter_enter}
            {{music_library get view_settings_provider} select_setting 1}
            {{music_library get view_settings_provider} select_setting_option $dx_song_select_sort_setting_1}
            {music_library report_sort_and_filters}
            {song_select_filter_panel filter_exit}
         }
      }
   )
   (dx_user_sort_scores
      {if {! $stored_score_sort}
         {set $stored_score_sort TRUE}
         {do
            ($loaded_inst {str_elem {{elem {{song_select_panel find gamertag.lbl} get_children} 0} get text} 5})
            ($tracked_cat FALSE)
            {switch $loaded_inst
               ((G B) {set $tracked_cat strings})
               ((D d) {set $tracked_cat drums})
               ((K k) {set $tracked_cat keys})
               ((V 2 3) {set $tracked_cat vocals})
               ((g b) {set $tracked_cat pro_strings})
            }
            {if $tracked_cat
               {if {!= {eval {var {sprint "dx_song_select_score_setting_" $tracked_cat}}} none}
                  {{music_library get view_settings_provider} refresh_all_settings}
                  {song_select_filter_panel filter_enter}
                  {{music_library get view_settings_provider} select_setting 2}
                  {{music_library get view_settings_provider} select_setting_option {eval {var {sprint "dx_song_select_score_setting_" $tracked_cat}}}}
                  {music_library report_sort_and_filters}
                  {song_select_filter_panel filter_exit}
               }
            }
         }
      }
   )
   (dx_more_info_load
      ($probe)
      {if_else {song.lst is_scrolling}
         {if {exists author_finder}
            {delete author_finder}
         }
         {if {&& $probe {== {{music_library get_highlighted_node} get_node_type} kNodeSong}}
            {do
               ($token {{music_library get_highlighted_node} get_token})
               ($song_path {sprint {song_mgr song_path $token} "/../../gen/songs.dtb"})
               {if
                  {||
                     {file_exists $song_path}
                     {song_mgr is_song_mounted $token}
                  }
                  {set $probeforfinder FALSE}
                  {$this dx_authorfinder}
               }
            }
         }
      }
   )
   (dx_authorfinder
      {if {! {gamemode in_mode trainer}}
         {do
            {if {exists author_finder}
               {delete author_finder}
            }
            {script_task kTaskUISeconds
               #ifndef RB3E
               (delay 0.75)
               #endif
               (name author_finder)
               (script
                  {$this dx_authorpoll {{music_library get_highlighted_node} get_token}}
                  {$this apply_authorfinder_metadata}
               )
            }
         }
      }
   )
   (dx_authorpoll
      ($shortname)
      #ifdef RB3E
      {$this rb3e_author_finder $shortname}
      #else
      {dx_author_finder_var_reset}
      {meta_performer set_song {symbol $shortname}}
      {set $dx_song_data_processed FALSE}
      {preload_panel dx_load_tip_and_author_finder}
      #endif
   )
   (rb3e_author_finder
      ($shortname)
      {set $authorvar FALSE}
      {set $namevar FALSE}
      {set $artistvar FALSE}
      {set $albumvar FALSE}
      {set $sourcevar FALSE}
      {set $genrevar FALSE}
      {set $subgenrevar FALSE}
      {if {rb3e_allowed}
         {do
            ($song {symbol $shortname})
            ($id {{song_mgr get_meta_data $song} id})
            {set $namevar {rb3e_get_song_name $id}}
            {set $artistvar {rb3e_get_artist $id}}
            {set $albumvar {rb3e_get_album $id}}
            {set $sourcevar {rb3e_get_origin $id}}
            {set $genrevar {rb3e_get_genre $id}}
            {set $subgenrevar {rb3e_get_genre $id}}
            ;{print_debug
            ;   {sprint
            ;      "NAME: " $namevar
            ;      " - ARTIST: " $artistvar
            ;      " - ALBUM: " $albumvar
            ;      " - SOURCE: " $sourcevar
            ;      " - GENRE: " $genrevar
            ;   }
            ;}
         }
      }
   )
   (apply_authorfinder_metadata
      {if $sourcevar
         {switch {{music_library get_highlighted_node} get_token}
            DX_SOURCE_SWITCH
         }
      }
      #ifdef RB3E
      {if {== $sourcevar rb1_dlc}
         {set $sourcevar dlc}
      }
      {if {== $sourcevar DOWNLOADED}
         {set $sourcevar customs}
      }
      #endif
      {if_else $sourcevar
         {dx_song_name_year_title.lbl set_token_fmt {localize $sourcevar}}
         {dx_song_name_year_title.lbl set_token_fmt os_blnk}
      }
      {if_else $authorvar
         {dx_song_name_album_title.lbl set_token_fmt stringify $authorvar}
         {dx_song_name_album_title.lbl set_token_fmt os_blnk}
      }

      {{song_select_panel find song_demo.grp} set_local_scale 0.16 1 0.2}
      {{song_select_panel find song_demo.grp} set_local_pos 70 0 -120}
      {if_else $sourcevar
         {do
            ($ext
               #ifdef HX_PS3 ".png_ps3" #endif
               #ifdef HX_XBOX ".png_xbox" #endif
               #ifdef HX_WII ".png_wii" #endif
            )
            ($exists {file_exists {sprint "ui/resource/game_origins/gen/" $sourcevar $ext}})
            ($image {if_else $exists $sourcevar "custom"})
            ($folder {if_else {== $dx_detected_platform platform_rpcs3} "game_origins_128" "game_origins"})
            {{song_select_panel find song_demo.grp} set_showing TRUE}
            {{song_select_panel find demo.tex} set_bitmap
               {sprint "ui/resource/" $folder "/" $image ".png"}
            }
            {{song_select_panel find song_demo.lbl} set_showing FALSE}
            {{song_select_panel find song_demo.lbl} set_token_fmt os_blnk}
         }
         {{song_select_panel find song_demo.grp} set_showing FALSE}
      }
   )
   (lbl_maker
      ($label $x $y $align $token)
      {do
         ;we handle diff labels slightly differently and its not worth it to have an
         ;extra func to handle that
         ($is_diff {has_substr $label "_diff"})
         ($diff_font
            #ifdef HX_WII
            instrument_icons_small
            #else
            instruments_icons_rings
            #endif
         )
         ($alt_font {if_else $is_diff $diff_font instrument_icons_small})
         ($size {if_else $is_diff 32 30})
         ($alt_size {if_else $is_diff 40 30})
         ($z_offset {if_else $is_diff 4 0})
         ($color_names (song_diff_color.color song_grey_color.color))
         ($colors ({pack_color 1 1 1} {pack_color 0.2 0.2 0.2}))
         {new BandLabel $label}
         {$label set resource_name pentatonic_outline}
         {$label set alt_style_enabled TRUE}
         {$label set alt_font_resource_name $alt_font}
         {foreach_int $i 0 1
            {do
               ($color_name {elem $color_names $i})
               ($color {elem $colors $i})
               {if {! {exists $color_name}}
                  {new UIColor $color_name}
                  {$color_name set color $color}
               }
            }
         }
         {$label set markup TRUE}
         {$label set_showing TRUE}
         {$label set_local_scale 1 1 1}
         {$label set_local_pos $x 0 $y}
         {$label set set_token_fmt $token}
         {$label set text_size $size}
         {$label set alt_text_size $alt_size}
         {$label set alt_z_offset $z_offset}
         {$label set fit_type 2}
         {$label set alignment $align}
         {$label set width 300}
         {$label set height 40}
         {$label set alpha 1}
         {live_diffs.grp add_object $label}
         {$label set_trans_parent live_diffs.grp}
      }
   )
   (dx_diff_maker
      {if {! {exists dx_guitar.lbl}}
         {foreach $entry
            (
               (dx_guitar.lbl -83.6 -84.8 kMiddleCenter dx_diff_1) (dx_guitar_diff.lbl -83.6 -85 kMiddleCenter os_blnk)
               (dx_bass.lbl -44 -84.8 kMiddleCenter dx_diff_0) (dx_bass_diff.lbl -44 -85 kMiddleCenter os_blnk)
               (dx_keys.lbl 36.1 -84.7 kMiddleCenter dx_diff_4) (dx_keys_diff.lbl 36 -85 kMiddleCenter os_blnk)
               (dx_drum.lbl -3.5 -85 kMiddleCenter dx_diff_2) (dx_drum_diff.lbl -4 -85 kMiddleCenter os_blnk)
               (dx_vocals.lbl 76.6 -85 kMiddleCenter dx_diff_3) (dx_vocals_diff.lbl 76.4 -85 kMiddleCenter os_blnk)
               (dx_real_guitar.lbl -83.4 -124.8 kMiddleCenter inst_icon_real_guitar) (dx_real_guitar_diff.lbl -84 -125 kMiddleCenter os_blnk)
               (dx_real_bass.lbl -43.4 -124.8 kMiddleCenter inst_icon_real_bass) (dx_real_bass_diff.lbl -44 -125 kMiddleCenter os_blnk)
               (dx_real_keys.lbl 36.5 -124.6 kMiddleCenter inst_icon_real_keys) (dx_real_keys_diff.lbl 36 -125 kMiddleCenter os_blnk)
               (dx_real_drum.lbl -3.4 -125 kMiddleCenter inst_icon_real_bass_drum) (dx_real_drum_diff.lbl -4 -125 kMiddleCenter os_blnk)
               (dx_real_vocals.lbl 76.1 -125.1 kMiddleCenter dx_diff_5) (dx_real_vocals_diff.lbl 76 -125 kMiddleCenter os_blnk)
               (dx_song_name_length.lbl 94 -53 kMiddleRight os_blnk) (dx_song_name_length_title.lbl 14 -53 kMiddleLeft os_blnk)
               (dx_song_name_album.lbl -5 -12 kMiddleCenter os_blnk) (dx_song_name_album_title.lbl -5 -22 kMiddleCenter os_blnk)
               (dx_song_name_genre.lbl 94 -35 kMiddleRight os_blnk) (dx_song_name_genre_title.lbl -103 -35 kMiddleLeft dx_genre)
               (dx_song_name_year.lbl -33 -53 kMiddleRight os_blnk) (dx_song_name_year_title.lbl -103 -53 kMiddleLeft dx_year)
            )
            {do
               ($label {elem $entry 0})
               ($x {elem $entry 1})
               ($y {elem $entry 2})
               ($align {elem $entry 3})
               ($token {elem $entry 4})
               {if {! {exists $label}}
                  {$this lbl_maker $label $x $y $align $token}
               }
            }
         }
      }
      {foreach $entry
         (
            dx_song_name_length.lbl dx_song_name_length_title.lbl
            dx_song_name_album.lbl dx_song_name_album_title.lbl
            dx_song_name_genre.lbl dx_song_name_genre_title.lbl
            dx_song_name_year.lbl dx_song_name_year_title.lbl
         )
         {$entry set alt_font_resource_name pentatonic_outline}
         {$entry set text_size 13}
         {$entry set alt_text_size 13}
      }

      {dx_real_vocals.lbl set alt_font_resource_name instruments_icons_rings}
      {dx_song_name_album_title.lbl set alt_font_resource_name buttons}

      {if_else $sourcevar
         {dx_song_name_album_title.lbl set_token_fmt {localize $sourcevar}}
         {dx_song_name_album_title.lbl set_token_fmt os_blnk}
      }
      {dx_song_name_album.lbl set width 200}
      {dx_song_name_album_title.lbl set width 200}
      {dx_song_name_album.lbl set height 10}
      {dx_song_name_album_title.lbl set height 10}
   )
   #define DX_SET_DIFFS
   (
      {if_else $dx_song_select_rings
         {drum_pro.idd set instrument_type band}
         {drum_pro.idd set instrument_type real_drum}
      }
      {guitar.idd set_song $token}
      {bass.idd set_song $token}
      {drum.idd set_song $token}
      {vocals.idd set_song $token}
      {harmony.idd set_song $token}
      {keys.idd set_song $token}
      {guitar_pro.idd set_song $token}
      {bass_pro.idd set_song $token}
      {drum_pro.idd set_song $token}
      {keys_pro.idd set_song $token}
      {band.idd set_song $token}
   )
   (dx_set_diff
      ($shortname)
      {do
         ($token $shortname)
         DX_SET_DIFFS
      }
   )
   #define ALL_SONG_SELECT_LABELS
   (
      (
         dx_guitar.lbl
         dx_guitar_diff.lbl
         dx_bass.lbl
         dx_bass_diff.lbl
         dx_keys.lbl
         dx_keys_diff.lbl
         dx_drum.lbl
         dx_drum_diff.lbl
         dx_vocals.lbl
         dx_vocals_diff.lbl
         dx_real_guitar.lbl
         dx_real_guitar_diff.lbl
         dx_real_bass.lbl
         dx_real_bass_diff.lbl
         dx_real_keys.lbl
         dx_real_keys_diff.lbl
         dx_real_drum.lbl
         dx_real_drum_diff.lbl
         dx_real_vocals.lbl
         dx_real_vocals_diff.lbl
         dx_song_name_length.lbl
         dx_song_name_length_title.lbl
         dx_song_name_album.lbl
         dx_song_name_album_title.lbl
         dx_song_name_genre.lbl
         dx_song_name_genre_title.lbl
         dx_song_name_year.lbl
         dx_song_name_year_title.lbl
      )
   )
   (fade_diffs
      ($key1 $key2)
      {if {! {exists diffs_fade.anim}}
         {new PropAnim diff_fade.anim}
         {do
            ($keys (0 1 0))
            {foreach $entry ALL_SONG_SELECT_LABELS
               {diff_fade.anim add_keys $entry (alpha) kPropFloat}
               {diff_fade.anim add_keys $entry (alt_alpha) kPropFloat}
               {foreach_int $i 0 {size $keys}
                  {diff_fade.anim set_key_val $entry (alt_alpha) $i {elem $keys $i}}
                  {diff_fade.anim set_key_val $entry (alpha) $i {elem $keys $i}}
               }
            }
         }
      }
      {diff_fade.anim set rate kTaskUISeconds}
      {diff_fade.anim set_frame 0}
      {diff_fade.anim animate (range $key1 $key2) (period 1) (units kTaskUISeconds)}
   )
   (rings_show
      ($show)
      {foreach $entry ALL_SONG_SELECT_LABELS
         {$entry set_showing $show}
      }
      {foreach $entry
         (guitar.idd bass.idd drum.idd vocals.idd harmony.idd keys.idd guitar_pro.idd bass_pro.idd drum_pro.idd keys_pro.idd)
         {$entry set_showing {! $show}}
      }

   )
   (call_diffs
      ($token)
      DX_SET_DIFFS
      {if_else $dx_song_select_rings
         {$this rings_show TRUE}
         {$this rings_show FALSE}
      }
   )
   (dx_diff_setter
      {{song_select_panel find song_demo.lbl} set_showing FALSE}
      {{song_select_panel find song_demo.lbl} set_token_fmt os_blnk}
      #ifdef RB3E
      {$this dx_more_info_load TRUE}
      #else
      {if_else $dx_more_info_toggle
         {do
            {dx_song_name_album_title.lbl set_token_fmt press_to_load_meta}
            {dx_song_name_year_title.lbl set_token_fmt os_blnk}
         }
         {do
            {dx_song_name_album_title.lbl set_token_fmt lb_waiting}
            {dx_song_name_year_title.lbl set_token_fmt lb_waiting}
         }
      }
      #endif
      {dx_song_name_album.lbl set_album_name
         {music_library get_highlighted_node}
      }
      {dx_song_name_genre_title.lbl set_token_fmt
         {{music_library get_highlighted_node} genre}
      }
      {dx_song_name_genre.lbl set_song_year
         {{music_library get_highlighted_node} year_released}
         {{music_library get_highlighted_node} year_released}
      }
      ;song_length
      {do
         ($ms {{music_library get_highlighted_node} get_total_ms})
         {dx_song_name_length.lbl set_token_fmt
            title_length_fmt
            (
               (title "")
               (minutes {int {/ $ms 60000}})
               (seconds {int {/ {mod $ms 60000} 1000}})
               (ms {mod $ms 1000})
            )
         }
      }

      {foreach $entry (guitar bass drum keys real_guitar real_bass real_keys real_vocals real_drum)
         {do
            ($idd_name
               {switch $entry
                  (real_guitar guitar_pro)
                  (real_bass bass_pro)
                  (real_keys keys_pro)
                  (real_drum drum) ;sorry
                  (real_vocals drum_pro) ;sorry
                  $entry
               }
            )
            ($label {sprint "dx_" $entry ".lbl"})
            ($label_diff {sprint "dx_" $entry "_diff.lbl"})
            ($idd {sprint $idd_name ".idd"})
            {$label_diff set_token_fmt
               {if_else {$idd get has_part}
                  {switch {$idd get difficulty}
                     DX_INS_DIFF_MAP
                  }
                  os_blnk
               }
            }
            {if_else {$idd get has_part}
               {$label set alt_text_color song_diff_color.color}
               {$label set alt_text_color song_grey_color.color}
            }
            {$label set_token_fmt {sprint "diff_icon_" $entry}}
         }
      }
      ;this is special
      {dx_vocals_diff.lbl set_token_fmt
         {switch {harmony.idd get num_vocal_parts}
            (
               (1 2 3)
               {switch {harmony.idd get difficulty}
                  DX_INS_DIFF_MAP
               }
            )
            (0 os_blnk)
         }
      }
      {switch {harmony.idd get num_vocal_parts}
         (0 {dx_vocals.lbl set alt_text_color song_grey_color.color})
         {dx_vocals.lbl set alt_text_color song_diff_color.color}
      }
      {dx_vocals.lbl set_token_fmt
         {switch {harmony.idd get num_vocal_parts}
            (0 diff_icon_solo_vocals)
            (1 diff_icon_solo_vocals)
            (2 diff_icon_harm2)
            (3 diff_icon_harm3)
         }
      }
   )
   (handle_to_task
      {script_task kTaskSeconds
         (name event_inactivity_backout)
         (delay 60)
         (script
            ;{dx_passive_messenger_symbol "PICK A GODDAMN SONG"}
            {ui abstract_wipe}
            {ui goto_screen meta_loading_main_screen}
         )
      }
   )
   (enter
      {if_else
         {&&
            $dx_event_mode
            {! $dx_in_show_flow}
            {! $dx_prep_show_flow}
            {! $dx_playing_a_show}
         }
         {if_else {exists event_inactivity_backout}
            {do
               {delete event_inactivity_backout}
               {$this handle_to_task}
            }
            {$this handle_to_task}
         }
         kDataUnhandled
      }
      {set $highscore_tracker ()}
      {resize $highscore_tracker 0}
      {push_back $highscore_tracker (fake_name_goes_here_lol faker)}
      {$this iterate
         UIList
         $l
         {$l set scroll_time 0}
      }
      {net_sync set_ui_state kNetUI_MusicLibrary}
      {set [should_go_to_art_maker] FALSE}
      {overshell add_sink $this}
      {input_mgr add_sink $this (input_user_left)}
      {rock_central add_sink $this (server_status_changed)}
      {platform_mgr add_sink
         $this
         (signin_changed profile_swapped storage_changed)
      }
      {music_library on_enter}
      {song.lst set_provider music_library}
      {music_library push_highlight_to_screen}
      {with song_select_details
         {leaderboard_types.lst set_data (lb_global lb_friends) 0 TRUE}
         {leaderboard_instruments.lst set_data (ORDERED_SCORE_TYPE_SYMBOLS) 0 TRUE}
      }
      {song.lst reveal}
      {if_else {&& [details_mode] {input_mgr has_user}}
         {$this show_details {input_mgr get_user}}
         {$this hide_details}
      }
      {setlist.lst set_provider {music_library get setlist_provider}}
      {$this refresh_setlist}
      {$this refresh_sort [waiting_for_sort]}
      {$this dx_user_sort}
      {$this dx_user_sort_scores}
      {dx_song_select_enter}
      {dx_presence_manager}
      ;{dx_sv_menu_logo_set sv4_panel}
      {set $dx_trainer_real_guitar FALSE}
      {set $dx_trainer_real_bass FALSE}
      {set $dx_trainer_real_keys FALSE}
      {set $dx_trainer_real_drum FALSE}
      {set $dx_skip_game_setup FALSE}
      {if {! $dx_prep_show_flow}
         {set $dx_playing_a_show FALSE}
      }
      #ifdef HX_WII
      {unless $dx_event_mode
         {if $dx_prep_show_flow
            {dx_modal_messenger dx_play_a_show_intro}
         }
      }
      #endif
      {overshell set_active_status kOvershellInShell}
      {overshell update_all}
      {unless $dx_song_select_noise {synth stop_all_sfx TRUE}} ; dx - removes the annoying noises in song select
      {$this dx_diff_maker}
      ;remove high song count warning
      ;{if
      ;   #ifdef HX_PS3
      ;   {&&
      ;      $dx_confirm_song_count
      ;      #ifdef REGION_EUROPE
      ;      {! {file_exists "GD:/dev_hdd0/game/BLES00986/USRDIR/dx_high_memory.dta"}}
      ;      #else
      ;      {! {file_exists "GD:/dev_hdd0/game/BLUS30463/USRDIR/dx_high_memory.dta"}}
      ;      #endif
      ;   }
      ;   #else
      ;   $dx_confirm_song_count
      ;   #endif
      ;   {set $high_song_count 4500}
      ;   {unless $rb3e_checked_song_count
      ;      {set $rb3e_checked_song_count TRUE}
      ;      {if {> {{song_select_panel find song.lst} num_data} $high_song_count}
      ;         {dx_modal_messenger_symbol high_song_count_warn}
      ;      }
      ;   }
      ;}
      {dx_admin_mode}
      #ifdef HX_WII
      {rnd set_in_game TRUE}
      #endif
      {if $dx_prep_show_flow
         {music_library.lbl set text_token dx_play_a_show}
      }
      {$this refresh_selected_song}
   )
   #define DX_PLAY_SET_SONG (
      {do
         {set $poopie ()}
         {resize $poopie 0}
         {push_back $poopie {{music_library get_highlighted_node} get_token}}
         {set $poopie {array $poopie}}
         {set $picked_song {elem $poopie 0}}
         {song.lst refresh}
         {set $num {random_int 0 {song.lst num_data}}}
         {song.lst set_selected_simulate_scroll $num}
         {set $dx_song_picked TRUE}
      }
   )
   #define DX_PLAY_ROLL_SONG (
      {do
         {song.lst refresh}
         {set $num {random_int 0 {song.lst num_data}}}
         {song.lst set_selected_simulate_scroll $num}
      }
   )
   (pick_a_song
      {do
         {set $num_tries 8}
         {set $dx_song_picked FALSE}
         {foreach_int $i 0 $num_tries
            {if_else
               {&&
                  {! $dx_song_picked}
                  {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
               }
               DX_PLAY_SET_SONG
               DX_PLAY_ROLL_SONG
            }
            {if {&& {== $num_tries $i} {! $dx_song_picked}}
               {set $picked_song {random_elem $rb3_songlist}}
            }
         }
         $picked_song
      }
   )
   (endurance_handler
      {if {|| $dx_customizer $dx_auto_endurance}
         {script_task kTaskSeconds
            (delay {if_else {== $dx_auto_endurance dx_selected} 1.5 0})
            (script
               {if_else {== $dx_auto_endurance dx_random}
                  {script_task kTaskSeconds
                     (delay 2)
                     (script
                        {song.lst refresh}
                        {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                        {script_task kTaskSeconds
                           (delay 2)
                           (script
                              {song.lst refresh}
                              {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                              {script_task kTaskSeconds
                                 (delay 2)
                                 (script
                                    {song.lst refresh}
                                    {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                                    {script_task kTaskSeconds
                                       (delay 2)
                                       (script
                                          {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                                             {do
                                                {gamemode set_mode qp_coop}
                                                {music_library select_highlighted_node {input_mgr get_user}}
                                             }
                                             {$task loop}
                                          }
                                       )
                                    }
                                 )
                              }
                           )
                        }
                     )
                  }
                  {do
                     {gamemode set_mode qp_coop}
                     {music_library select_highlighted_node {input_mgr get_user}}
                  }
               }
            )
         }
      }
   )
   (TRANSITION_COMPLETE_MSG
      {dx_restore_online_transitions}
      {$this endurance_handler}
      {if [resume_setlist_edit]
         {ui push_screen edit_setlist_screen}
         {set [resume_setlist_edit] FALSE}
      }
      {if [should_go_to_art_maker]
         {ui abstract_wipe}
         {ui goto_screen patch_select_practice_space_screen}
         {set [should_go_to_art_maker] FALSE}
      }
   )
   (exit
      ;{if $dx_event_mode
      ;   {if {exists event_inactivity_backout}
      ;      {delete event_inactivity_backout}
      ;   }
      ;}
      {set [details_mode] FALSE}
      {music_library on_exit}
      {input_mgr remove_sink $this input_user_left}
      {overshell remove_sink $this}
      {session remove_sink $this}
      {rock_central remove_sink $this server_status_changed}
      {platform_mgr remove_sink $this signin_changed}
      {platform_mgr remove_sink $this profile_swapped}
      {platform_mgr remove_sink $this storage_changed}
      #ifdef HX_WII
      {rnd set_in_game FALSE}
      #endif
   )
   (BUTTON_DOWN_MSG
      {if_else
         {&&
            $dx_event_mode
            {! $dx_in_show_flow}
            {! $dx_prep_show_flow}
            {! $dx_playing_a_show}
         }
         {if_else {exists event_inactivity_backout}
            {do
               {delete event_inactivity_backout}
               {$this handle_to_task}
            }
            {$this handle_to_task}
         }
         kDataUnhandled
      }
      {if_else {ui in_transition}
         TRUE
         {if_else [details_mode]
            {handle_ret
               ($this details_mode_button_down $user $raw_button $action $pad_num)
            }
            {handle_ret
               ($this main_mode_button_down $user $raw_button $action $pad_num)
            }
         }
      }
   )
   (on_button_held
      ($user $raw_button $action $pad_num)
      {if_else {ui in_transition}
         TRUE
         {if_else [details_mode]
            {handle_ret
               ($this details_mode_button_down $user $raw_button $action $pad_num)
            }
            {handle_ret
               ($this main_mode_button_held $user $raw_button $action $pad_num)
            }
         }
      }
   )
   (details_buttons
      (details_1.btn details_2.btn details_3.btn details_4.btn details_5.btn)
   )
   (highlight_button
      ($token)
      {foreach $button [details_buttons]
         {set $button {song_select_details find $button}}
         {if {== {$button get text_token} $token}
            {$this set_focus $button}
         }
      }
   )
   (find_button
      ($token)
      {do
         ($ret "")
         {set $ret ""}
         {foreach $button [details_buttons]
            {set $button {song_select_details find $button}}
            {if {== {$button get text_token} $token}
               {set $ret $button}
            }
         }
         $ret
      }
   )
   (VIRTUAL_KEYBOARD_RESULT_MSG
      {if {&& $ok $this {! {== $text ""}}}
         ;sanitize player input
         {set $got_text {fix_special_chars $text}}
         ;force song sort
         {{music_library get view_settings_provider} refresh_all_settings}
         {song_select_filter_panel filter_enter}
         {{music_library get view_settings_provider} select_setting 1}
         {{music_library get view_settings_provider} select_setting_option 0}
         {music_library report_sort_and_filters}
         {song_select_filter_panel filter_exit}
         ;jump to top of song select
         {music_library skip_to_shortcut 1}
         #ifdef NOT_YET_MERGED
            #ifdef RB3E
            {rb3e_search_song_name {sprint $got_text}}
            #endif
         #endif
         #ifndef NOT_YET_MERGED
         ;jump to the first letter shortcut
         {switch {str_elem $got_text 0}
            (
               (0 1 2 3 4 5 6 7 8 9)
               {music_library skip_to_shortcut 1}
            )
            ("a" {music_library skip_to_shortcut 2})
            ("b" {music_library skip_to_shortcut 3})
            ("c" {music_library skip_to_shortcut 4})
            ("d" {music_library skip_to_shortcut 5})
            ("e" {music_library skip_to_shortcut 6})
            ("f" {music_library skip_to_shortcut 7})
            ("g" {music_library skip_to_shortcut 8})
            ("h" {music_library skip_to_shortcut 9})
            ("i" {music_library skip_to_shortcut 10})
            ("j" {music_library skip_to_shortcut 11})
            ("k" {music_library skip_to_shortcut 12})
            ("l" {music_library skip_to_shortcut 13})
            ("m" {music_library skip_to_shortcut 14})
            ("n" {music_library skip_to_shortcut 15})
            ("o" {music_library skip_to_shortcut 16})
            ("p" {music_library skip_to_shortcut 17})
            ("q" {music_library skip_to_shortcut 18})
            ("r" {music_library skip_to_shortcut 19})
            ("s" {music_library skip_to_shortcut 20})
            ("t" {music_library skip_to_shortcut 21})
            ("u" {music_library skip_to_shortcut 22})
            ("v" {music_library skip_to_shortcut 23})
            ("w" {music_library skip_to_shortcut 24})
            ("x" {music_library skip_to_shortcut 25})
            ("y" {music_library skip_to_shortcut 26})
            ("z" {music_library skip_to_shortcut 27})
         }
         ;start searching for the song
         {foreach_int $i 1 {- {song.lst num_data} 2}
            {if_else
               {||
                  {&&
                     {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                     {has_substr
                        {fix_special_chars {{music_library get_highlighted_node} title}}
                        $got_text
                     }
                  }
                  {&&
                     {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                     {has_substr
                        {fix_special_chars {{music_library get_highlighted_node} get_token}}
                        $got_text
                     }
                  }
               }
               kDataUnhandled
               {if
                  {||
                     {== {{music_library get_highlighted_node} get_node_type} kNodeSubheader}
                     {== {{music_library get_highlighted_node} get_node_type} kNodeShortcut}
                     {== {{music_library get_highlighted_node} get_node_type} kNodeHeader}
                     {== {{music_library get_highlighted_node} get_node_type} kNodeFunction}
                     {&&
                        {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                        {!
                           {has_substr
                              {fix_special_chars {{music_library get_highlighted_node} get_token}}
                              $got_text
                           }
                        }
                     }
                     {&&
                        {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                        {!
                           {has_substr
                              {fix_special_chars {{music_library get_highlighted_node} title}}
                              $got_text
                           }
                        }
                     }
                  }
                  {if_else {== {song.lst selected_pos} {- {song.lst num_data} 3}}
                     {song.lst set_selected_simulate_scroll 0}
                     {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 1}}
                  }
               }
            }
         }
         #endif
      }
   )
   (SELECT_MSG
      {unless {|| $dx_customizer $dx_auto_endurance}
         {with song_select_details
            {cond
               ({== $component details_1.btn}
                  {music_library select_highlighted_node $user}
                  {song_select_panel hide_details}
               )
               ({==
                     $component
                     {song_select_panel get lb_list}}
                  {with song_select_panel
                     {$this select_lb_row {[lb_list] selected_pos} $user}
                  }
               )
               ({==
                     {$component get text_token}
                     review_song}
                  {buttons.grp set_showing FALSE}
                  {details_bottom.grp set_showing FALSE}
                  {review.grp set_showing TRUE}
                  {song_select_panel set_focus
                     {$this find {sprintf "review_%i.rvw" {review.rvw get score}}}
                  }
               )
               ({==
                     {$component get text_token}
                     delete_song}
                  {if_else {session_mgr is_local}
                     {do
                        {delete_content_confirm_screen set
                           content
                           #ifdef HX_WII
                           {{music_library get_highlighted_node} id}
                           #else
                           {song_mgr content_name {{music_library get_highlighted_node} id}}
                           #endif
                        }
                        {ui push_screen delete_content_confirm_screen}
                     }
                     {do
                        {push_basic_confirm_dialog_small delete_song_no_online}
                     }
                  }
               )
               ({==
                     {$component get text_token}
                     leaderboard}
                  {buttons.grp set_showing FALSE}
                  {details_bottom.grp set_showing FALSE}
                  {performance.grp set_showing TRUE}
                  {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSetlist}
                     {do
                        {song_select_panel set lb_list {object performance.lst}}
                        {leaderboard_instruments.lst set_showing FALSE}
                        {leaderboard_help.ihp set_showing FALSE}
                     }
                     {do
                        {song_select_panel set lb_list {object performance.lst}}
                        {leaderboard_instruments.lst set_showing TRUE}
                        {leaderboard_help.ihp set_showing TRUE}
                        {set $score_sym
                           {elem (SCORE_TYPE_SYMBOLS) {music_library active_score_type}}
                        }
                        {set $score_index 0}
                        {find_elem (ORDERED_SCORE_TYPE_SYMBOLS) $score_sym $score_index}
                        {leaderboard_instruments.lst set_selected $score_index}
                     }
                  }
                  {with song_select_panel
                     {$this refresh_leaderboard $user}
                     {$this set_focus [lb_list]}
                  }
               )
               ({==
                     {$component get text_token}
                     practice_or_train_song}
                  {cond
                     ({!
                           {gamemode get library_trainers_allowed}}
                        {push_basic_confirm_dialog no_trainers_in_some_modes}
                     )
                     ({!
                           {session_mgr is_local}}
                        {push_basic_confirm_dialog no_trainers_when_online}
                     )
                     {submenu_buttons.grp set_showing TRUE}
                     {buttons.grp set_showing FALSE}
                     {submenu01.btn set text_token practice_song}
                     {submenu02.btn set text_token train_song}
                     {song_select_panel set_focus submenu01.btn}
                  }
               )
               ({==
                     {$component get text_token}
                     practice_song}
                  {cond
                     ({!
                           {gamemode get library_trainers_allowed}}
                        {push_basic_confirm_dialog no_trainers_in_some_modes}
                     )
                     ({!
                           {session_mgr is_local}}
                        {push_basic_confirm_dialog no_trainers_when_online}
                     )
                     {gamemode set_mode qp_practice}
                     {training_mgr set_user $user}
                     {critical_user_listener set_critical_user $user}
                     {training_mgr set_return_info song_select_screen pause_quit}
                     {practice_sel_section_panel clear_state}
                     {music_library clear_setlist}
                     {music_library set_making_setlist FALSE}
                     {music_library select_highlighted_node $user}
                     {unless {music_library setlist_size}
                        {gamemode set_mode qp_coop}
                     }
                  }
               )
               ({==
                     {$component get text_token}
                     train_song}
                  {cond
                     ({!
                           {gamemode get library_trainers_allowed}}
                        {push_basic_confirm_dialog no_trainers_in_some_modes}
                     )
                     ({!
                           {session_mgr is_local}}
                        {push_basic_confirm_dialog no_trainers_when_online}
                     )
                     ({!
                           {profile_mgr get_profile $user}}
                        {push_basic_confirm_dialog trainer_must_signin}
                     )
                     {cond
                        ({==
                              {$user get_controller_type}
                              kControllerKeys}
                           {gamemode set_mode pro_song_lessons_keyboard}
                        )
                        ({==
                              {$user get_controller_type}
                              kControllerRealGuitar}
                           {gamemode set_mode pro_song_lessons_real_guitar}
                        )
                     }
                     {training_mgr set_user $user}
                     {training_mgr set_minimum_difficulty kDifficultyEasy}
                     {training_mgr set_return_info song_select_screen pause_quit}
                     {critical_user_listener set_critical_user $user}
                     {song_select_panel set trainer_from_main_menu kControllerNone}
                     {music_library clear_setlist}
                     {music_library set_making_setlist FALSE}
                     {music_library select_highlighted_node $user}
                  }
               )
               ({==
                     {$component get text_token}
                     overshell_gamercard}
                  {{music_library get_highlighted_node} view_gamercard $user}
               )
               ({==
                     {$component get text_token}
                     edit_setlist}
                  {if
                     {edit_setlist_panel edit_setlist
                        $user
                        {{music_library get_highlighted_node} get_setlist}
                     }
                     {song_select_panel hide_details}
                     {ui push_screen edit_setlist_screen}
                  }
               )
               ({==
                     {$component get text_token}
                     delete_setlist}
                  {ui push_screen delete_setlist_confirm_screen}
               )
               ({==
                     {$component get text_token}
                     share_setlist}
                  {do
                     ($item {music_library get_highlighted_node})
                     ($setlist {$item get_setlist})
                     {$setlist set_shared TRUE}
                     {{$setlist get_owner_profile} setlist_changed $setlist}
                     DX_AUTOSAVE
                     {$component set text_token unshare_setlist}
                  }
               )
               ({==
                     {$component get text_token}
                     unshare_setlist}
                  {do
                     ($item {music_library get_highlighted_node})
                     ($setlist {$item get_setlist})
                     {$setlist set_shared FALSE}
                     {{$setlist get_owner_profile} setlist_changed $setlist}
                     DX_AUTOSAVE
                     {$component set text_token share_setlist}
                  }
               )
               kDataUnhandled
            }
         }
      }
   )
   (refresh_leaderboard
      ($user)
      {do
         ($item {music_library get_highlighted_node})
         ($id {$item id})
         ($type {{song_select_details find leaderboard_types.lst} selected_data})
         {$this lb_in_progress}
         {set $score_index 0}
         {find_elem
            (SCORE_TYPE_SYMBOLS)
            {{song_select_details find leaderboard_instruments.lst} selected_sym}
            $score_index
         }
         {[lb_list] set_provider
            {$this get_leaderboard $user $score_index $id $type}
         }
      }
   )
   (details_mode_button_down
      ($user $raw_button $action $pad_num)
      {cond
         ({== $action kAction_ViewModify}
            {with song_select_details
               {if {performance.grp showing}
                  {leaderboard_types.lst scroll 1 $user}
               }
            }
         )
         ({== $action kAction_ShellOption}
            {with song_select_details
               {cond
                  ({&&
                        {performance.grp showing}
                        {leaderboard_instruments.lst showing}}
                     {leaderboard_instruments.lst scroll 1 $user}
                  )
                  ({details_bottom.grp showing}
                     {instruments.lst scroll 1 $user}
                  )
               }
            }
         )
         (
            #ifdef HX_WII
            {== $action {$this get_option_button}}
            #else
            {== $action kAction_Option}
            #endif
            {$this hide_details}
         )
         #ifdef HX_WII
         ({'||'
               {== $action kAction_Left}
               {== $action kAction_Right}}
            {do
               ($node {music_library get_highlighted_node})
               ($type {$node get_node_type})
               ($new_score
                  {if_else {== $action kAction_Left}
                     {mod {- {header_review.rvw get score} 1} 6}
                     {mod {'+' {header_review.rvw get score} 1} 6}
                  }
               )
               {if {&& {! [waiting_for_sort]} {song_review.grp showing}}
                  {{profile_mgr get_primary_profile} set_song_review {$node id} $new_score}
                  {$node update_review}
                  {header_review.rvw set_values $new_score TRUE}
                  {set [reviews_dirty] TRUE}
               }
            }
         )
         #endif
         ({== $action kAction_Cancel}
            {with song_select_details
               {cond
                  ({submenu_buttons.grp showing}
                     {submenu_buttons.grp set_showing FALSE}
                     {buttons.grp set_showing TRUE}
                     {if_else {== {submenu01.btn get text_token} delete_song_confirm}
                        {song_select_panel highlight_button delete_song}
                        {song_select_panel highlight_button practice_or_train_song}
                     }
                  )
                  ({review.grp showing}
                     {buttons.grp set_showing TRUE}
                     {details_bottom.grp set_showing TRUE}
                     {review.grp set_showing FALSE}
                     {song_select_panel highlight_button review_song}
                  )
                  ({==
                        {song_select_panel focus_name}
                        "performance.lst"}
                     {performance.grp set_showing FALSE}
                     {buttons.grp set_showing TRUE}
                     {details_bottom.grp set_showing {! {music_library viewing_setlists}}}
                     {song_select_panel highlight_button leaderboard}
                  )
                  {song_select_panel hide_details}
               }
            }
         )
         ({&&
               {== $action kAction_Confirm}
               {startswith
                  {$this focus_name}
                  "review_"}}
            {with song_select_details
               {do
                  ($score {{$this find {song_select_panel focus_name}} get score})
                  {if {!= $score {{music_library get_highlighted_node} get_review}}
                     {{profile_mgr get_primary_profile} set_song_review
                        {{music_library get_highlighted_node} id}
                        $score
                     }
                     {if {{music_library get_highlighted_node} update_review}
                        {{song_select_panel find header_review.rvw} set_values $score TRUE}
                        {music_library re_sort by_review}
                     }
                  }
                  {review.rvw set score $score}
                  {buttons.grp set_showing TRUE}
                  {review.grp set_showing FALSE}
                  {song_select_panel highlight_button review_song}
               }
            }
         )
         ({==
               {$this focus_name}
               "performance.lst"}
            {with song_select_details
               {switch $action
                  (kAction_Up
                     {if_else
                        {&&
                           {performance.lst num_data}
                           {== {performance.lst selected_data} 0}
                           {song_select_panel scroll_lb_up}
                        }
                        {song_select_panel lb_in_progress}
                        kDataUnhandled
                     }
                  )
                  (kAction_Down
                     {if_else
                        {&&
                           {performance.lst num_data}
                           {== {performance.lst selected_data} {- {performance.lst num_data} 1}}
                           {song_select_panel scroll_lb_down}
                        }
                        {song_select_panel lb_in_progress}
                        kDataUnhandled
                     }
                  )
                  #ifdef HX_PS3
                  (kAction_Confirm TRUE)
                  #endif
                  #ifdef HX_WII
                  (kAction_Confirm TRUE)
                  #endif
                  kDataUnhandled
               }
            }
         )
         ({== $action kAction_Up}
            {do
               ($ret FALSE)
               ($focused {song_select_details find {$this focus_name}})
               {if {== {$focused name} "submenu02.btn"}
                  {set $ret {song_select_details find "submenu01.btn"}}
               }
               {if {! $ret}
                  {foreach $button [details_buttons]
                     {set $button {song_select_details find $button}}
                     {if {== {$button get nav_down} $focused}
                        {set $ret $button}
                     }
                  }
               }
               {if {! $ret}
                  {foreach_int $i 0 6
                     {do
                        ($rvw {song_select_details find {sprintf "review_%i.rvw" $i} TRUE})
                        {if {== {$rvw get nav_down} $focused}
                           {set $ret $rvw}
                           {set $i 5}
                        }
                     }
                  }
               }
               {if_else $ret
                  {do
                     {play_instr_sfx $user button_toggle}
                     {$this set_focus $ret}
                  }
                  kDataUnhandled
               }
            }
         )
         (TRUE kDataUnhandled)
      }
   )
   (main_mode_button_down
      ($user $raw_button $action $pad_num)
      {cond
         ({&&
               {== $action kAction_ViewModify}
               {!
                  {song.lst is_scrolling}}}
            {unless {music_library viewing_setlists}
               {{music_library get view_settings_provider} refresh_all_settings}
               {song_select_filter_panel filter_enter}
            }
         )
         ({== $action kAction_Cancel}
            {if_else $dx_prep_show_flow
               {do
                  {set $dx_in_show_flow FALSE}
                  {set $dx_prep_show_flow FALSE}
                  {set $dx_playing_a_show FALSE}
                  {overshell end_override_flow kOverrideFlow_SongSettings TRUE}
                  {ui sync_screen meta_loading_main_screen 0}
               }
               {cond
                  ({&&
                     {music_library get_making_setlist}
                     {>
                        {music_library setlist_size}
                        0}}
                     {music_library remove_last_song_from_setlist}
                  )
                  ({&&
                     {music_library get_making_setlist}
                     {!
                        {music_library get_forced_setlist}}}
                     {music_library set_making_setlist FALSE}
                  )
                  ({session_mgr is_local}
                     {ui go_back_screen {music_library get_back_screen} $user}
                  )
                  ({is_leader_local}
                     {if_else {machine_mgr all_machines_have_same_net_ui_state}
                        {ui sync_screen {music_library get_back_screen} 0}
                        {ui_event_mgr trigger_event remote_not_ready}
                     }
                  )
                  (TRUE {ui_event_mgr trigger_event remote_exit})
               }
            }
         )
         ({&&
               {== $action kAction_Confirm}
               {!
                  {song.lst is_scrolling}}}
            {unless {'||' {ui in_transition} [waiting_for_sort]}
               {do
                  ($type {{music_library get_highlighted_node} get_node_type})
                  {if
                     {'||'
                        {== $type kNodeFunction}
                        {== $type kNodeSetlist}
                        {! {music_library viewing_setlists}}
                     }
                     {play_instr_sfx $user button_select}
                     {if {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                        {if {> {{music_library get_highlighted_node} get_score} 0}
                           {foreach $entry $highscore_tracker
                              {if {!= {elem $entry 0} {{music_library get_highlighted_node} get_token}} ;check if the state isnt in the list
                                 {set $pushtime_highscore TRUE} ;get ready to push it
                              }
                           }
                           {foreach $entry $highscore_tracker
                              {if {== {elem $entry 0} {{music_library get_highlighted_node} get_token}} ;check the list again, and if it is in the list
                                 {set $pushtime_highscore FALSE} ;dont push it
                              }
                           }
                           {if $pushtime_highscore ;if we need to push
                              {push_back
                                 $highscore_tracker
                                 ({{music_library get_highlighted_node} get_token}
                                    {{music_library get_highlighted_node} get_score}
                                 )
                              } ;push_back a new state into the array
                           }
                           {set $highscore_tracker {array $highscore_tracker}}
                           ;{dx_passive_messenger_symbol {sprint $highscore_tracker}}
                        }
                     }
                     {music_library select_highlighted_node $user}
                  }
               }
            }
         )
         ({&&
               #ifdef HX_WII
               {==
                  $action
                  {$this get_option_button}}
               #else
               {== $action kAction_Option}
               #endif
               {!
                  {song.lst is_scrolling}}}
            {do
               ($type {{music_library get_highlighted_node} get_node_type})
               {if
                  {&&
                     {! [waiting_for_sort]}
                     {'||' {== $type kNodeSong} {== $type kNodeSetlist}}
                  }
                  {if_else $dx_event_mode
                     kDataUnhandled
                     {$this show_details $user}
                  }
               }
            }
         )
         ({&&
               #ifdef HX_WII
               {'||'
                  {'||'
                     {==
                        $action
                        {$this get_option_button}}
                     {== $action kAction_Right}}
                  {== $action kAction_Left}}
               #else
               {== $action kAction_ShellOption}
               #endif
               {!
                  {song.lst is_scrolling}}}
            {do
               ($node {music_library get_highlighted_node})
               ($type {$node get_node_type})
               #ifdef HX_WII
               ($new_score
                  {if_else {== $action kAction_Left}
                     {mod {- {header_review.rvw get score} 1} 6}
                     {mod {'+' {header_review.rvw get score} 1} 6}
                  }
               )
               #else
               ($new_score {mod {'+' {header_review.rvw get score} 1} 6})
               #endif
               {if {&& {! [waiting_for_sort]} {song_review.grp showing}}
                  {{profile_mgr get_primary_profile} set_song_review {$node id} $new_score}
                  {$node update_review}
                  {header_review.rvw set_values $new_score TRUE}
                  {set [reviews_dirty] TRUE}
               }
            }
         )
         ({&&
               #ifdef HX_WII
               {==
                  $action
                  {$this get_page_down_button}}
               #else
               {== $action kAction_PageDown}
               #endif
               {!
                  {song.lst is_scrolling}}}
            {unless [waiting_for_sort]
               {song.lst set_scroll_user $user}
               {if_else {&& {music_library can_headers_be_selected} {== {music_library get_current_sort_name} by_artist}}
                  {do
                     {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 1}}
                     {set $pause_scroll_sound TRUE}
                     {foreach_int $i 0 50
                        {if_else
                           {&&
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeShortcut}
                              ;{!= {{music_library get_highlighted_node} get_node_type} kNodeSubheader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeHeader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeFunction}
                           }
                           {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 1}}
                           kDataUnhandled
                        }
                        {if
                           {&&
                              {== $i 49}
                              ;{!= {{music_library get_highlighted_node} get_node_type} kNodeSubheader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeShortcut}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeHeader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeFunction}
                           }
                           {music_library skip_to_next_shortcut}
                        }
                     }
                     {set $pause_scroll_sound FALSE}
                  }
                  {music_library skip_to_next_shortcut}
               }
            }
         )
         #ifndef HX_WII
         ({&&
            {== $action kAction_Left}
            {!
               {song.lst is_scrolling}}}
         {unless
            [waiting_for_sort]
            {song.lst set_scroll_user $user}
            {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 1}}
            {set $pause_scroll_sound TRUE}
            {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 9}}
            {set $pause_scroll_sound FALSE}}) ; dx scroll the list up by 10 slots when pressing left dpad
         ({&&
            {== $action kAction_Right}
            {!
               {song.lst is_scrolling}}}
         {unless
            [waiting_for_sort]
            {song.lst set_scroll_user $user}
            {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 1}}
            {set $pause_scroll_sound TRUE}
            {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 9}}
            {set $pause_scroll_sound FALSE}}) ; dx scroll the list down by 10 slots when pressing right dpad
         #endif
         ({&&
               #ifdef HX_WII
               {==
                  $action
                  {$this get_page_up_button}}
               #else
               {== $action kAction_PageUp}
               #endif
               {!
                  {song.lst is_scrolling}}}
            {unless [waiting_for_sort]
               {song.lst set_scroll_user $user}
               {if_else {&& {music_library can_headers_be_selected} {== {music_library get_current_sort_name} by_artist}}
                  {do
                     {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 1}}
                     {set $pause_scroll_sound TRUE}
                     {foreach_int $i 0 50
                        {if_else
                           {&&
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeSubheader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeShortcut}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeHeader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeFunction}
                           }
                           {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 1}}
                           kDataUnhandled
                        }
                        {if
                           {&&
                              {== $i 49}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeSubheader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeShortcut}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeHeader}
                              {!= {{music_library get_highlighted_node} get_node_type} kNodeFunction}
                           }
                           {music_library skip_to_prev_shortcut}
                        }
                     }
                     {set $pause_scroll_sound FALSE}
                  }
                  {music_library skip_to_prev_shortcut}
               }
            }
         )
         ({'||'
               {== $action kAction_Up}
               {== $action kAction_Down}}
            {unless [waiting_for_sort]
               {handle
                  (song.lst button_down $user $raw_button $action $pad_num)
               }
            }
         )
         (TRUE kDataUnhandled)
      }
   )
   (main_mode_button_held
      ($user $raw_button $action $pad_num)
      {cond
         #ifndef RB3E
         ({&& {== $action kAction_ShellOption} $dx_more_info_toggle}
            {$this dx_more_info_load TRUE}
         )
         #endif
         ({== $action kAction_Confirm}
            {unless {'||' {ui in_transition} [waiting_for_sort]}
               {do
                  ($already_making_setlist {music_library get_making_setlist})
                  ($node {music_library get_highlighted_node})
                  ($on_function {== {$node get_node_type} kNodeFunction})
                  ($on_random {== {$node get_token} random_song})
                  ($starting_size {music_library setlist_size})
                  {cond
                     ({&&
                           {music_library can_headers_be_selected}
                           {! $already_making_setlist}
                           {'||'
                              {! $on_function}
                              $on_random}}
                        {music_library clear_setlist}
                        {music_library set_making_setlist TRUE}
                        {if {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                           {if {> {{music_library get_highlighted_node} get_score} 0}
                              {foreach $entry $highscore_tracker
                                 {if {!= {elem $entry 0} {{music_library get_highlighted_node} get_token}} ;check if the state isnt in the list
                                    {set $pushtime_highscore TRUE} ;get ready to push it
                                 }
                              }
                              {foreach $entry $highscore_tracker
                                 {if {== {elem $entry 0} {{music_library get_highlighted_node} get_token}} ;check the list again, and if it is in the list
                                    {set $pushtime_highscore FALSE} ;dont push it
                                 }
                              }
                              {if $pushtime_highscore ;if we need to push
                                 {push_back
                                    $highscore_tracker
                                    ({{music_library get_highlighted_node} get_token}
                                       {{music_library get_highlighted_node} get_score}
                                    )
                                 } ;push_back a new state into the array
                              }
                              {set $highscore_tracker {array $highscore_tracker}}
                              ;{dx_passive_messenger_symbol {sprint $highscore_tracker}}
                           }
                        }
                        {music_library select_highlighted_node $user}
                     )
                     ({&&
                           $already_making_setlist
                           {'||'
                              {! $on_function}
                              $on_random}}
                        {if
                           {'||'
                              {== {music_library get_max_setlist_size} 0}
                              {== {music_library get_max_setlist_size} {music_library setlist_size}}
                           }
                           {music_library play_setlist}
                        }
                     )
                     (TRUE {music_library select_highlighted_node $user})
                  }
                  {play_instr_sfx $user button_select}
               }
            }
         )
         ({== $action kAction_Cancel}
            {if_else $dx_prep_show_flow
               {do
                  {set $dx_in_show_flow FALSE}
                  {set $dx_prep_show_flow FALSE}
                  {set $dx_playing_a_show FALSE}
                  {overshell end_override_flow kOverrideFlow_SongSettings TRUE}
                  {ui sync_screen meta_loading_main_screen 0}
               }
               {if_else
                  {&&
                     {music_library get_making_setlist}
                     {! {music_library get_forced_setlist}}
                  }
                  {do
                     {music_library clear_setlist}
                     {music_library set_making_setlist FALSE}
                  }
                  {if_else {! {session is_local}}
                     {if_else {is_leader_local}
                        {if_else {machine_mgr all_machines_have_same_net_ui_state}
                           ;dx virtual keyboard
                           {virtual_keyboard show_keyboard
                              $user
                              17
                              {localize song_search}
                              {localize song_search_desc}
                              {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                                 {{music_library get_highlighted_node} title}
                                 {localize song_search_title}
                              }
                              $this
                           }
                           ;{ui sync_screen {music_library get_back_screen} 0}
                           {ui_event_mgr trigger_event remote_not_ready}
                        }
                        {ui_event_mgr trigger_event remote_exit}
                     }
                     ;dx, fallback to search on hold red lmao
                     ;{virtual_keyboard show_keyboard
                     ;   $user
                     ;   17
                     ;   {localize song_search}
                     ;   {localize song_search_desc}
                     ;   {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                     ;      {{music_library get_highlighted_node} title}
                     ;      {localize song_search_title}
                     ;   }
                     ;   $this
                     ;}
                     {do
                        {set $keyboard_ticket {array ($user)}}
                        {ui push_screen dx_search_screen}
                     }
                     ;{ui
                     ;   go_back_screen
                     ;   {music_library get_back_screen}
                     ;   $user}
                  }
               }
            }
         )
         ({'||'
               #ifdef HX_WII
               {==
                  $action
                  {$this get_page_up_button}}
               {==
                  $action
                  {$this get_page_down_button}}
               #else
               {== $action kAction_PageUp}
               {== $action kAction_PageDown}
               #endif
            }
            {unless {'||' {ui in_transition} [waiting_for_sort]}
               {song_select_shortcut_panel shortcut_enter
                  groups
                  $user
                  {music_library get_current_shortcut_ix}
               }
            }
         )
      }
   )
   (SCROLL_MSG
      {if {exists author_finder}
         {delete author_finder}
      }
      {set $probeforfinder TRUE}
      {cond
         ({&&
               {== $component song.lst}
               {!
                  {ui in_transition}}}
            {music_library set_highlight_ix {song.lst selected_pos} FALSE}
            {if [reviews_dirty]
               {set [reviews_dirty] FALSE}
               DX_AUTOSAVE
            }
         )
         ({==
               $component
               {song_select_details find instruments.lst}}
            {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
               {$this refresh_star_grid
                  {profile_mgr get_primary_profile}
                  {{music_library get_highlighted_node} id}
                  {{song_select_details find instruments.lst} selected_sym}
               }
               {$this refresh_setlist_scores
                  {profile_mgr get_primary_profile}
                  {{song_select_details find instruments.lst} selected_sym}
               }
            }
         )
         ({==
               $component
               {song_select_details find leaderboard_instruments.lst}}
            {$this refresh_leaderboard $user}
         )
         ({==
               $component
               {song_select_details find leaderboard_types.lst}}
            {$this lb_in_progress}
            {$this set_leaderboard_mode
               {{song_select_details find leaderboard_types.lst} selected_data}
            }
         )
      }
   )
   (FOCUS_MSG
      {if {&& $new_focus {startswith {$new_focus name} "review_"}}
         {{song_select_details find review_desc.lbl} set
            text_token
            {sprintf "review_%i_desc" {$new_focus get score}}
         }
      }
   )
   (highlight_node_at_ix
      ($ix)
      {song.lst refresh}
      {song.lst set_selected_simulate_scroll $ix}
   )
   (set_details_button_config
      ($array)
      {with song_select_details
         {foreach_int $i 0 {size $array}
            {do
               ($button {object {sprintf "details_%i.btn" {'+' $i 1}}})
               {$button set text_token {elem $array $i}}
               {$this enable $button}
            }
         }
         {foreach_int $i {size $array} {size {song_select_panel details_buttons}}
            {do
               ($button {object {sprintf "details_%i.btn" {'+' $i 1}}})
               {$button set text_token ""}
               {$this disable $button}
            }
         }
      }
   )
   (show_details
      ($user)
      {do
         {drum_pro.idd set instrument_type real_drum}
         {if {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
            {drum_pro.idd set_song {{music_library get_highlighted_node} get_token}}
         }
      }
      {set [details_mode] TRUE}
      {input_mgr set_user $user}
      {if_else {ui in_transition}
         {details_show.trg play_end_of_anims}
         {details_show.trg trigger}
      }
      {if_else {ui in_transition}
         {setlist_hide.trg play_end_of_anims}
         {setlist_hide.trg trigger}
      }
      {with song_select_details
         {buttons.grp set_showing TRUE}
         {details_bottom.grp set_showing TRUE}
         {submenu_buttons.grp set_showing FALSE}
         {performance.grp set_showing FALSE}
         {review.grp set_showing FALSE}
         {stars.grp set_showing FALSE}
         {profile_msg.grp set_showing FALSE}
         {setlist_scores.grp set_showing FALSE}
      }
      {do
         ($item {music_library get_highlighted_node})
         ($type {$item get_node_type})
         ($length_ms {$item get_total_ms})
         {switch $type
            (kNodeSong
               {do
                  ($config {array 0})
                  {cond
                     ({gamemode in_mode practice}
                        {push_back $config practice_song}
                     )
                     ({gamemode in_mode trainer}
                        {push_back $config train_song}
                     )
                     {if_else {music_library get_making_setlist}
                        {push_back $config enqueue_song}
                        {push_back $config play_song}
                     }
                  }
                  {unless {'||' {gamemode in_mode practice} {gamemode in_mode trainer}}
                     {if_else
                        {&&
                           #ifdef HX_WII
                           TRUE
                           #else
                           ;always load trainers lol
                           TRUE
                           ;{! {$item is_ugc}}
                           #endif
                           {'||'
                              {&&
                                 {== {$user connected_controller_type} kControllerKeys}
                                 {$item has_part real_keys}
                              }
                              {&&
                                 {== {$user connected_controller_type} kControllerRealGuitar}
                                 {'||' {$item has_part real_guitar} {$item has_part real_bass}}
                              }
                           }
                        }
                        {push_back $config practice_or_train_song}
                        {push_back $config practice_song}
                     }
                  }
                  {if {profile_mgr has_primary_profile}
                     {push_back $config review_song}
                  }
                  {push_back $config leaderboard}
                  {if {$item is_download}
                     {push_back $config delete_song}
                  }
                  {$this set_details_button_config $config}
               }
            )
            (kNodeSetlist
               {do
                  ($config {array 0})
                  {push_back $config play_set}
                  #ifdef HX_XBOX
                  {if {$item has_viewable_gamercard}
                     {push_back $config overshell_gamercard}
                  }
                  #endif
                  {if {$item is_battle}
                     {push_back $config leaderboard}
                  }
                  {if
                     {&&
                        {$item is_local_setlist}
                        {$item is_profile_owner {profile_mgr get_profile $user}}
                     }
                     {push_back $config edit_setlist}
                     {push_back $config delete_setlist}
                     {if
                        {&&
                           {platform_mgr is_user_signed_into_live
                              {{profile_mgr get_primary_profile} get_associated_user}
                           }
                           {rock_central is_online}
                        }
                        {if_else {{$item get_setlist} is_shared}
                           {push_back $config unshare_setlist}
                           {push_back $config share_setlist}
                        }
                     }
                  }
                  {$this set_details_button_config $config}
               }
            )
         }
         {switch $type
            (kNodeSong
               {do
                  ($instrument {$user connected_controller_sym})
                  {if {== $instrument real_guitar}
                     {set $instrument guitar}
                  }
                  {$this refresh_star_grid
                     {profile_mgr get_primary_profile}
                     {$item id}
                     $instrument
                  }
                  {with song_select_details
                     {album.lbl set_album_name $item}
                     {genre.lbl set text_token {$item genre}}
                     {description.lbl set text_token ""}
                     {year.lbl set_song_year {$item year_released} {$item year_recorded}}
                     {details_artist_name.lbl set_artist_name $item}
                     {rating.lbl set_rating_icon {$item rating}}
                     {rating_description.lbl set
                        text_token
                        {sprintf "rating_%i" {$item rating}}
                     }
                     {rating.grp set_showing TRUE}
                     {details_song_name_length.lbl set_token_fmt
                        title_length_fmt
                        (
                           (title {$item title})
                           (minutes {int {/ $length_ms 60000}})
                           (seconds {int {/ {mod $length_ms 60000} 1000}})
                           (ms {mod $length_ms 1000})
                        )
                     }
                     {instruments.lst set_data
                        (guitar bass drum keys vocals)
                        0
                        TRUE
                     }
                     {instruments.lst set_selected $instrument}
                     {review.rvw set_values {$item get_review} FALSE}
                  }
               }
            )
            (kNodeSetlist
               {do
                  ($score_type guitar)
                  {switch {$user connected_controller_sym}
                     (
                        (guitar real_guitar)
                        {set $score_type guitar}
                     )
                     (vocals {set $score_type st_vocals})
                     (keys {set $score_type keys})
                     (drum {set $score_type drum})
                  }
                  {with song_select_details
                     {album.lbl set text_token {$item get_setlist_type_sym}}
                     {genre.lbl set_battle_instrument_str {$item get_record}}
                     {description.lbl set_setlist_description {$item get_record}}
                     {year.lbl set text_token ""}
                     {details_artist_name.lbl set_artist_name $item}
                     {rating.grp set_showing FALSE}
                     {if {$item is_battle}
                        {details_bottom.grp set_showing FALSE}
                     }
                     {if_else {>= $length_ms 3600000}
                        {details_song_name_length.lbl set_token_fmt
                           title_length_hour_fmt
                           (
                              (title {$item get_title})
                              (hours {int {/ $length_ms 3600000}})
                              (minutes {int {/ {mod $length_ms 3600000} 60000}})
                              (seconds {int {/ {mod $length_ms 60000} 1000}})
                              (ms {mod $length_ms 1000})
                           )
                        }
                        {details_song_name_length.lbl set_token_fmt
                           title_length_fmt
                           (
                              (title {$item get_title})
                              (minutes {int {/ $length_ms 60000}})
                              (seconds {int {/ {mod $length_ms 60000} 1000}})
                              (ms {mod $length_ms 1000})
                           )
                        }
                     }
                     {instruments.lst set_data (SCORE_TYPE_SYMBOLS) 0 TRUE}
                     {instruments.lst set_selected $score_type}
                  }
                  {setlist_main.lst stop_auto_scroll}
                  {$this refresh_setlist_scores
                     {profile_mgr get_primary_profile}
                     $score_type
                  }
               }
            )
         }
      }
      {$this set_focus {song_select_details find details_1.btn}}
   )
   (refresh_star_grid
      ($profile $song_id $instrument)
      {with song_select_details
         {if $profile
            {foreach_int $i kDifficultyEasy kNumDifficulties
               {switch $instrument
                  (guitar
                     {{basic_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreGuitar $i}
                        5
                     }
                     {{pro_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreRealGuitar $i}
                        5
                     }
                     {{basic_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreGuitar $i}
                     }
                     {{pro_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreRealGuitar $i}
                     }
                  )
                  (bass
                     {{basic_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreBass $i}
                        5
                     }
                     {{pro_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreRealBass $i}
                        5
                     }
                     {{basic_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreBass $i}
                     }
                     {{pro_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreRealBass $i}
                     }
                  )
                  (keys
                     {{basic_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreKeys $i}
                        5
                     }
                     {{pro_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreRealKeys $i}
                        5
                     }
                     {{basic_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreKeys $i}
                     }
                     {{pro_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreRealKeys $i}
                     }
                  )
                  (drum
                     {{basic_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreDrum $i}
                        5
                     }
                     {{pro_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreRealDrum $i}
                        5
                     }
                     {{basic_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreDrum $i}
                     }
                     {{pro_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreRealDrum $i}
                     }
                  )
                  (vocals
                     {{basic_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreVocals $i}
                        5
                     }
                     {{pro_stars.set
                           get
                           (objects $i)} set_values
                        {$profile get_stars $song_id kScoreHarmony $i}
                        5
                     }
                     {{basic_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreVocals $i}
                     }
                     {{pro_pcts.set
                           get
                           (objects $i)} set_token_fmt
                        eg_percent_format
                        {$profile get_accuracy $song_id kScoreHarmony $i}
                     }
                  )
               }
            }
            {stars.grp set_showing TRUE}
         }
         {profile_msg.grp set_showing {! {stars.grp showing}}}
         {instrument_icon.lbl set icon {cnv_instrumenttoicon $instrument 0}}
      }
   )
   (refresh_setlist_scores
      ($profile $score_type)
      {with song_select_details
         {if $profile
            {setlist_scores.lst set_provider setlist_scores.lst}
            {do
               ($provider {music_library get setlist_scores_provider})
               {$provider set_profile $profile}
               {$provider set_score_type $score_type}
               {$provider refresh_scores}
               {setlist_scores.lst set_provider $provider}
            }
            {setlist_scores.grp set_showing TRUE}
            {setlist_scores.lst auto_scroll}
            {instrument_icon.lbl set
               icon
               {get_font_char_from_score_type {instruments.lst selected_pos} 0}
            }
         }
         {profile_msg.grp set_showing {! {setlist_scores.grp showing}}}
      }
   )
   (hide_details
      {if {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
         {drum_pro.idd set_song {{music_library get_highlighted_node} get_token}}
         {$this call_diffs {{music_library get_highlighted_node} get_token}}
      }
      {set [details_mode] FALSE}
      {if {! {gamemode in_mode practice}}
         {input_mgr clear_user}
      }
      {if_else {ui in_transition}
         {details_hide.trg play_end_of_anims}
         {details_hide.trg trigger}
      }
      {if {music_library get_making_setlist}
         {if_else {ui in_transition}
            {setlist_show.trg play_end_of_anims}
            {setlist_show.trg trigger}
         }
      }
      {do
         ($item {music_library get_highlighted_node})
         ($type {$item get_node_type})
         {switch $type
            (kNodeSetlist {setlist_main.lst auto_scroll})
         }
      }
      {$this set_focus song.lst}
   )
   (input_user_left
      {if [details_mode]
         {$this hide_details}
      }
   )
   (server_status_changed
      ($connected)
      {if [details_mode]
         {foreach $btn (share_setlist unshare_setlist)
            {set $btn {$this find_button $btn}}
            {if_else $connected
               {if $btn
                  {song_select_details enable $btn}
               }
               {if $btn
                  {if {== {$btn get_state} kComponentFocused}
                     {$this highlight_button delete_setlist}
                  }
                  {song_select_details disable $btn}
               }
            }
         }
      }
   )
   (signin_changed
      {if [details_mode]
         {$this hide_details}
      }
   )
   (profile_swapped
      {if [details_mode]
         {$this hide_details}
      }
   )
   (refresh_screen_header
      {cond
         ({gamemode in_mode campaign}
            {header_left.grp set_showing FALSE}
            {header_tour.grp set_showing FALSE}
            {header_goals.grp set_showing TRUE}
            {goal_title.lbl set text_token {campaign get_current_goal}}
            {goal_desc.lbl set text_token {campaign get_current_goal_description}}
            {career.pic set tex_file {campaign get_current_goal_icon}}
         )
         ({gamemode in_mode tour}
            {header_tour.grp set_showing TRUE}
            {header_left.grp set_showing FALSE}
            {header_goals.grp set_showing FALSE}
            {tour_title.lbl set text_token {{tour progress} get_tour_name}}
            {do
               ($filter {tour get_current_filter_name})
               ($song_count {{tour performer} get_gig_songcount})
               {if_else {== $song_count 1}
                  {tour_desc.lbl set_token_fmt set_list_tour_title_singular $filter}
                  {tour_desc.lbl set_token_fmt set_list_tour_title $filter $song_count}
               }
            }
         )
         (TRUE
            {header_left.grp set_showing TRUE}
            {header_goals.grp set_showing FALSE}
            {header_tour.grp set_showing FALSE}
            {$this update_title}
         )
      }
   )
   (update_title
      {if_else #ifdef RB3E {&& {rb3e_allowed} $rb3e_party_ip} #else FALSE #endif
         {music_library.lbl set_token_fmt stringify {sprint {rb3e_local_ip} {localize rb3e_default_port}}}
         {if_else {modifier_mgr is_modifier_active mod_brutalmode}
            {music_library.lbl set text_token mod_brutalmode}
            {if_else {music_library viewing_setlists}
               {music_library.lbl set text_token setlist_browser}
               {if_else {music_library get_making_setlist}
                  {music_library.lbl set text_token {music_library get_making_setlist_token}}
                  {music_library.lbl set text_token {music_library get_title_token}}
               }
            }
         }
      }
   )
   (on_change_setlist_mode
      {if_else {music_library get_making_setlist}
         {do
            {setlist_show.trg trigger}
            {header_setlist.trg trigger}
         }
         {do
            {setlist_hide.trg trigger}
            {if_else {modifier_mgr is_modifier_active mod_brutalmode}
               {do
                  {music_library.lbl set text_token mod_brutalmode}
                  {header_setlist.trg trigger}
               }
               {header_normal.trg trigger}
            }
         }
      }
      {$this refresh_screen_header}
   )
   (refresh_top
      {do
         ($item {music_library get_highlighted_node})
         ($token {$item get_token})
         {album_overlay.mesh set_showing FALSE}
         {cond
            ({&&
                  {==
                     kNodeSong
                     {$item get_node_type}}
                  {!
                     {song_mgr is_song_mounted $token}}}
               {album_art.pic set tex_file "ui/image/blank_album_art_keep.png"}
               {album_art.pic set mesh {album_art.pic get mesh}}
            )
            ({==
                  kNodeStoreSong
                  {$item get_node_type}}
               {album_art.pic set tex_file "ui/image/blank_album_art_keep.png"}
               {album_art.pic set mesh {album_art.pic get mesh}}
               {music_library load_store_art {$item get_offer} $this}
            )
            ({==
                  kNodeSetlist
                  {$item get_node_type}}
               {cond
                  ({$item is_net_setlist}
                     {if_else {music_library net_setlist_art_ready $token}
                        {do
                           {album_overlay.mesh set_showing TRUE}
                           {album_overlay.mat set
                              diffuse_tex
                              {music_library get_net_setlist_art $token}
                           }
                           {album_art.pic set tex_file "ui/image/canvas_keep.png"}
                           {album_art.pic set mesh {album_art.pic get mesh}}
                        }
                        {do
                           {album_art.pic set tex_file {$item album_art_path}}
                           {album_art.pic set mesh {album_art.pic get mesh}}
                           {music_library refresh_net_setlist_art}
                        }
                     }
                  )
                  ({$item is_local_setlist}
                     {if_else {$item get_art_tex}
                        {do
                           {album_overlay.mesh set_showing TRUE}
                           {album_overlay.mat set diffuse_tex {$item get_art_tex}}
                           {album_art.pic set tex_file "ui/image/canvas_keep.png"}
                           {album_art.pic set mesh {album_art.pic get mesh}}
                        }
                        {do
                           {album_art.pic set tex_file {$item album_art_path}}
                           {album_art.pic set mesh {album_art.pic get mesh}}
                        }
                     }
                  )
                  {album_art.pic set tex_file {$item album_art_path}}
                  {album_art.pic set mesh {album_art.pic get mesh}}
               }
            )
            {do
               ($ext
                  #ifdef HX_PS3 "png_ps3" #endif
                  #ifdef HX_XBOX "png_xbox" #endif
                  #ifdef HX_WII "png_wii" #endif
               )
               {if_else {file_exists {sprint "songs/updates/" $token "/gen/" $token "_keep." $ext}}
                  {album_art.pic set tex_file {sprint "songs/updates/" $token "/" $token "_keep.png"}}
                  {album_art.pic set tex_file {$item album_art_path}}
               }
            }
            {album_art.pic set mesh {album_art.pic get mesh}}
         }
      }
   )
   (art_loaded
      {if
         {&&
            {$this is_up}
            {== {{music_library get_highlighted_node} get_node_type} kNodeStoreSong}
         }
         {album.mat set diffuse_tex {music_library get_store_art}}
      }
   )
   (refresh_songlist
      {song.lst refresh}
      {if {!= {song_select_shortcut_panel get shortcut_mode} none}
         {{song_select_shortcut_panel find shortcut.lst} refresh}
      }
   )
   (update_helpbar
      {do
         ($item {music_library get_highlighted_node})
         ($type {$item get_node_type})
         ($making_setlist {music_library get_making_setlist})
         ($setlist_can_finish
            {&& {> {music_library setlist_size} 0} {is_leader_local}}
         )
         ($primary "")
         ($secondary "")
         ($option "")
         ($platform_has_keyboard {|| {== $dx_detected_platform platform_rpcs3} {== $dx_detected_platform platform_ps3} {== $dx_detected_platform platform_wii}})
         {set $primary
            {switch $type
               (kNodeFunction select)
               (
                  (kNodeHeader kNodeSubheader)
                  {if_else {music_library viewing_setlists}
                     ""
                     {if_else $making_setlist
                        enqueue_all
                        play_all
                     }
                  }
               )
               (kNodeSong
                  {if_else $making_setlist
                     enqueue_song
                     play_song
                  }
               )
               (kNodeSetlist play_set)
               (kNodeStoreSong
                  {if_else {music_library is_downloading $item}
                     ""
                     purchase_song
                  }
               )
            }
         }
         {set $secondary
            {switch $type
               (kNodeFunction
                  {cond
                     ({&&
                           {==
                              {$item get_token}
                              random_song}
                           $making_setlist
                           $setlist_can_finish}
                        hold_finish_setlist
                     )
                     ({&&
                           {==
                              {$item get_token}
                              random_song}
                           {! $making_setlist}
                           {music_library can_headers_be_selected}}
                        hold_make_setlist
                     )
                     (TRUE "")
                  }
               )
               (
                  (kNodeHeader kNodeSubheader kNodeStoreSong kNodeSong)
                  {cond
                     ({&& $making_setlist $setlist_can_finish} hold_finish_setlist)
                     ({&&
                           {! $making_setlist}
                           {music_library can_headers_be_selected}}
                        hold_make_setlist
                     )
                     (TRUE "")
                  }
               )
               (kNodeSetlist "")
            }
         }
         {unless $dx_event_mode
            {set $option
               {switch $type
                  ((kNodeFunction kNodeHeader kNodeSubheader kNodeStoreSong) "")
                  ((kNodeSong kNodeSetlist) details)
               }
            }
         }
         {cond
            ({&& $primary $secondary}
               {help.ihp set_config ((kAction_Confirm $primary $secondary))}
            )
            ({'||' $primary $secondary}
               {help.ihp set_config
                  (
                     (kAction_Confirm
                        {if_else $primary
                           $primary
                           $secondary
                        }
                     )
                  )
               }
            )
            (TRUE {help.ihp set_config ()})
         }
         {help.ihp set_action_token kAction_Cancel help_search}
         {if $option
            {help.ihp set_action_token kAction_Option $option}
         }
         {if_else $platform_has_keyboard
            {do
               {help.ihp set (config 1 secondary_token) help_search_keyboard}
               {help.ihp set spacing 255}
               {help.ihp set_local_pos_index 0 -395}
            }
            {do
               {help.ihp set spacing 240}
               {help.ihp set_local_pos_index 0 -380}
            }
         }
         {settings.ihp set_local_pos_index 0 180}
      }
   )
   (set_mini_leaderboard_showing
      ($showing)
      {if_else $showing
         {do
            {leaderboard_show.trg trigger}
            {difficulty_hide.trg trigger}
         }
         {do
            {leaderboard_hide.trg trigger}
            {difficulty_show.trg trigger}
         }
      }
   )
   (refresh_selected_song
      {$this refresh_top}
      {do
         ($item {music_library get_highlighted_node})
         ($type {$item get_node_type})
         {if_else {&& {== $type kNodeSong} {profile_mgr has_primary_profile}}
            {do
               {header_review.rvw set_values {$item get_review} TRUE}
               {song_review.grp set_showing TRUE}
               ;{$this
               ;   dx_get_highscore ;???? doesnt exist
               ;   {profile_mgr get_primary_profile}
               ;}
            }
            {song_review.grp set_showing FALSE}
         }
         {if_else {'||' {== $type kNodeSong} {== $type kNodeStoreSong}}
            {do
               {leaderboard.mld update_leaderboard
                  {$item id}
                  {music_library active_score_type}
               }
               {$this restart_leaderboard_timer}
            }
            {$this cancel_leaderboard_timer}
         }
         {if_else {== $type kNodeSetlist}
            {do
               {setlist_main.lst set_provider {music_library get setlist_scores_provider}}
               {setlist_main.lst set_selected 0}
               {setlist_main.lst auto_scroll}
               {setlist_creator.lbl set_setlist_owner {$item get_record}}
               {if_else {$item is_battle}
                  {setlist_info.lbl set_battle_time_left {$item get_battle_time_left}}
                  {setlist_info.lbl set_song_count {$item get_song_count}}
               }
               {right_side_setlist.grp set_showing TRUE}
            }
            {right_side_setlist.grp set_showing FALSE}
         }
         #ifdef HX_PS3
         {ps3_storelogo.grp set_showing {== $type kNodeStoreSong}}
         #endif
         {switch $type
            (kNodeSong
               {do
                  ($token {$item get_token})
                  {$this call_diffs $token}
                  {if $dx_song_select_rings
                     {$this dx_diff_setter}
                  }
                  {live_diffs.grp set_showing TRUE}
               }
            )
            (kNodeStoreSong
               {guitar.idd set_rank {$item get_data (rank guitar)}}
               {bass.idd set_rank {$item get_data (rank bass)}}
               {drum.idd set_rank {$item get_data (rank drum)}}
               {vocals.idd set_rank {$item get_data (rank vocals)}}
               {harmony.idd set_rank {$item get_data (rank vocals)}}
               {keys.idd set_rank {$item get_data (rank keys)}}
               {guitar_pro.idd set_rank {$item get_data (rank real_guitar)}}
               {bass_pro.idd set_rank {$item get_data (rank real_bass)}}
               {drum_pro.idd set_rank {$item get_data (rank drum)}}
               {keys_pro.idd set_rank {$item get_data (rank real_keys)}}
               {harmony.idd set
                  num_vocal_parts
                  {if_else {$item has_data vocal_parts}
                     #ifdef HX_WII
                     {$item vocal_parts}
                     #else
                     {$item get_data (vocal_parts)}
                     #endif
                     0
                  }
               }
               {live_diffs.grp set_showing TRUE}
            )
            {live_diffs.grp set_showing FALSE}
         }
         {song_demo.grp set_showing
            {&& {== $type kNodeSong} {song_mgr is_demo {$item id}}}
         }
         {$this update_helpbar}
      }
   )
   (refresh_filter {status.lbl set_music_library_status})
   (refresh_sort
      ($waiting)
      {if_else {music_library viewing_setlists}
         {do
            {header_setlist.trg trigger}
            {switch_to_setlist_mode.trg trigger}
            {setlist_hide.trg trigger}
            {$this refresh_screen_header}
         }
         {do
            {switch_to_song_mode.trg trigger}
            {if_else {music_library get_making_setlist}
               {setlist_show.trg trigger}
               {if_else {modifier_mgr is_modifier_active mod_brutalmode}
                  {do
                     {music_library.lbl set text_token mod_brutalmode}
                     {header_setlist.trg trigger}
                  }
                  {header_normal.trg trigger}
               }
            }
            {$this refresh_screen_header}
         }
      }
      {sort_wait.grp set_showing $waiting}
      {set [waiting_for_sort] $waiting}
      {status.lbl set_music_library_status}
   )
   (refresh_setlist
      {setlist.lst refresh}
      {setlist.lst set_selected {min {music_library setlist_size} 99}}
      {$this update_helpbar}
   )
   (refresh_summary
      {if_else {music_library has_header_data}
         {do
            ($profile {profile_mgr get_primary_profile})
            ($tex {$profile get_picture_tex})
            {careerscore.scr set_values
               {music_library header_career_instrument_mask}
               {music_library header_career_score}
            }
            {careerstars.sd set_values
               {music_library header_career_stars}
               {music_library header_possible_stars}
            }
            {gamertag.lbl set_icon_and_profile_name
               {music_library active_score_type}
               $profile
            }
            {if_else $tex
               {profile_picture.mat set diffuse_tex $tex}
               {profile_picture.mat set diffuse_tex default_profile_picture.tex}
            }
            {stats.grp set_showing TRUE}
         }
         {stats.grp set_showing FALSE}
      }
   )
   (move_on_quickplay
      {ui sync_screen {music_library get_next_screen} 0}
      {dx_send_leader_song_speed}
   )
   (storage_changed
      {if [details_mode]
         {$this hide_details}
      }
      #ifdef HX_WII
      {if
         {&&
            {!= {ui transition_screen} deleting_content_screen}
            {!= {ui current_screen} deleting_content_screen}
         }
         {content_mgr start_refresh}
      }
      #else
      {if {content_mgr refresh_done}
         {content_mgr start_refresh}
      }
      #endif
   )
   (show_setlist_save_dialog {ui push_screen setlist_save_screen})
   (show_missing_setlist_songs_dialog
      ($num_songs)
      {missing_setlist_songs_screen set num_songs $num_songs}
      {ui push_screen missing_setlist_songs_screen}
   )
   (lb_in_progress
      {if {$this is_up}
         {with song_select_details
            {view_gamer_card.ihp set_showing FALSE}
            {lb_status.lbl set text_token lb_waiting}
            {performance.lst set showing FALSE}
         }
      }
   )
   (lb_success
      ($noresults $unranked $unplayed)
      {if {$this is_up}
         {with song_select_details
            {view_gamer_card.ihp set_showing FALSE}
            {{song_select_panel get lb_list} refresh}
            {song_select_panel set_to_starting_lb_ix {song_select_panel get lb_list}}
            {if_else $noresults
               {do
                  {lb_status.lbl set text_token lb_noresults}
                  {performance.lst set showing FALSE}
               }
               {do
                  {performance.lst set showing TRUE}
                  {lb_status.lbl set text_token ""}
                  #ifdef HX_XBOX
                  {view_gamer_card.ihp set_showing TRUE}
                  #endif
               }
            }
         }
      }
   )
   (lb_failure
      {if {$this is_up}
         {with song_select_details
            {view_gamer_card.ihp set_showing FALSE}
            {performance.lst set showing FALSE}
            #ifdef HX_WII
            {lb_status.lbl set text_token lb_error_live}
            #else
            {if_else {platform_mgr is_connected}
               {lb_status.lbl set text_token lb_error}
               {lb_status.lbl set text_token lb_error_live}
            }
            #endif
         }
      }
   )
}
{new BandScreen
   song_select_screen
   (panels
      meta
      postsong_sfx_panel
      #ifdef HX_WII
      sv4_panel
      #else
      DX_SV4_PANEL
      #endif
      song_select_panel
      song_select_shortcut_panel
      song_select_filter_panel
   )
   (focus song_select_panel)
   (enter
      {if $dx_pas_sort_changed
         {song_select_filter_panel filter_enter}
         {{music_library get view_settings_provider} select_setting 1}
         {{music_library get view_settings_provider} select_setting_option 0}
         {music_library report_sort_and_filters}
         {song_select_filter_panel filter_exit}
         {set $dx_pas_sort_changed FALSE}
      }
      {song_select_panel set_showing TRUE}
      {sv4_panel set_showing TRUE}
   )
   (net_sync_scroll FALSE)
}