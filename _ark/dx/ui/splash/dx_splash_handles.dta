#define DX_SPLASH_HANDLES
(
   (dx_splash_screen_enter
      {if_else {&& {! $dx_black_menu} $dx_splash_screen}
         {ui goto_screen splash_screen} ; dx - goes to the splash screen if enabled
         {ui goto_screen meta_loading_main_screen} ; dx - skips the intro movie instantly if disabled
      }
      {saveload_mgr activate} ; dx - load save file early
      #ifdef HX_PS3
      {content_mgr start_refresh} ; dx - start content enumeration early, this causes cache issues on 360 so ps3 only
      #endif
   )
)
#define DX_YELLOW_SPLASH_TEXT
(
      {do
         ($num 178) ;increment this number based on the number of dx_splash entries in locale
         {dx_splashtext.btn set text_token {sprint "dx_splash_" {random_int 1 $num}}} ; dx - set a random splash text
      }
      {dx_splashtext.btn set_local_pos 197.5 0 140}
      {dx_splashtext.btn set_local_rot 35 12.5 -25}
      {new PropAnim splash_text.anim}
      {splash_text.anim add_keys dx_splashtext.btn (position) kPropVector3}

      {splash_text.anim set_key_val dx_splashtext.btn (position) 0 (197.5 0 140)}
      {splash_text.anim set_key_val dx_splashtext.btn (position) 1 (197.5 -50 140)}
      {splash_text.anim set_key_val dx_splashtext.btn (position) 2 (197.5 0 140)}
      {splash_text.anim set_interp_type dx_splashtext.btn (position) kPropHermite}

      {splash_text.anim set rate kTaskUISeconds}
      {splash_text.anim set_frame 0}
      {splash_text.anim animate (loop 0 2) (period 1) (units kTaskUISeconds)}
)

#define DX_NEW_SPLASH_OBJ
(
   {new UIColor custom.color}
   {custom.color set color {pack_color 0.96 0.90 0.28}}

   {new BandButton dx_splashtext.btn}
   {dx_splashtext.btn set resource_name "pentatonic_outline"}
   {dx_splashtext.btn set_showing TRUE}
   {dx_splashtext.btn set_local_scale 1 1 1}
   {dx_splashtext.btn set_local_pos 0 0 -140}
   {dx_splashtext.btn set text_size 30}
   {dx_splashtext.btn set fit_type 2}
   {dx_splashtext.btn set alignment kMiddleCenter}
   {dx_splashtext.btn set width 400}
   {dx_splashtext.btn set height 30}
   {dx_splashtext.btn set alpha 1}
   {dx_splashtext.btn set color_override custom.color}
   {text.grp add_object dx_splashtext.btn}

)
#define DX_INTERNAL_SPLASH_TEXT ;this handles the beta splash easter egg
(
   {custom.color set color {pack_color 1 0 0}}
   {dx_splashtext.btn set color_override custom.color}
   {dx_splashtext.btn set_token_fmt {sprint "INTERNAL BUILD ONLY.  DO NOT DISTRIBUTE IN ANY FORM."}}
   {set $btn_width {dx_splashtext.btn get width}}
   {dx_splashtext.btn set text_size 22.5}
   {dx_splashtext.btn set width {+ 200 $btn_width}}
   {dx_splashtext.btn set_local_pos 0 0 63} ;figure out how to get the right cords somehow, this is a guess
   {start.btn set_showing TRUE}
)

#define DX_SPLASH_PANEL_HANDLES
(
   (lbl_maker
      ($label $x $y $align $content)
      {new BandLabel $label}
      {$label set resource_name "pentatonic_outline"}
      {$label set alt_style_enabled TRUE}
      {$label set alt_font_resource_name pentatonic_outline}
      {if {! {exists dx_build.color}}
         {new UIColor dx_build.color}
         {dx_build.color set color {pack_color 1 1 1}}
      }
      {$label set markup TRUE}
      {$label set_showing TRUE}
      {$label set_local_scale 1 1 1}
      {$label set_local_pos $x 0 $y}
      {$label set set_token_fmt $content}
      {$label set text_size 15}
      {$label set alt_text_size 15}
      {$label set alt_z_offset -4}
      {$label set fit_type 2}
      {$label set alignment $align}
      {$label set width 300}
      {$label set height 40}
      {$label set alpha 1}
      ;{$label set color_override now_play_color.color}
      {text.grp add_object $label}
   )
   (dx_splash_panel_enter 
      DX_NEW_SPLASH_OBJ

      {start.btn set_showing FALSE}

      {set $dx_random_number {random_int 0 500}}
      {if {== $dx_random_number 1}
         {if $debug_mode
            {set $dx_splash_beta_egg TRUE}
         }
      }
      #ifndef _SHIP
      {set $dx_splash_beta_egg FALSE} ;test
      #endif
      {if {== $dx_splash_beta_egg FALSE}
         DX_YELLOW_SPLASH_TEXT
      }
      #ifdef _SHIP
      {if {== $dx_splash_beta_egg TRUE}
         DX_INTERNAL_SPLASH_TEXT
      }
      #endif
      ; set main menu logo based on season
      {if_else $dx_seasonal_logos
         {do
            {get_date_time $year $month $day $hour $minute $second}
            {cond
               ({&& {== $month 2} {>= $day 7} {<= $day 17}} {set $dx_season valentines})
               ({&& {== $month 4} {== $day 1}} {set $dx_season aprilfools})
               ({== $month 6} {set $dx_season pride})
               ({== $month 10} {set $dx_season halloween})
               ({== $month 12} {set $dx_season christmas})
               (else {set $dx_season standard})
            }
         }
         {set $dx_season standard}
      }
      {{{{sv8_panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap {sprint "dx/custom_textures/_additional_textures/logo-" $dx_season #ifdef RB3E "-rb3e" #endif ".png"}}
      {if {== $dx_season halloween}
         {{{{sv8_panel find sv8_a} find cityscape_ao} find moon.tex} set_bitmap "dx/custom_textures/_additional_textures/blood_moon.png"}
      }
      {if {== $dx_splash_beta_egg TRUE}
         {{{{sv8_panel find sv8_a} find cityscape_ao} find logo.mesh} set_showing FALSE}
      }
      {if {== $dx_splash_beta_egg TRUE}
         {set $dx_random_number {random_int 0 2}}
         {if {== $dx_random_number 1}
            {rb3.lbl set_showing TRUE}
            {rb3.lbl set_token_fmt "Rock Band 3 DX"}
         }
      }
      {if {== $dx_season christmas}
         {with {{sv8_panel find sv8_a} find cityscape_ao}
            {new ParticleSys snow.part}
            {snow.part set fancy TRUE}
            {snow.part set_local_pos -1300 1300 1256}
            {new Mat snow.mat}
            {snow.mat copy star_particle.mat kCopyShallow}
            {new Tex snow.tex}
            {snow.tex set_bitmap "dx/custom_textures/_additional_textures/snow_particle.png"}
            {snow.mat set diffuse_tex snow.tex}
            {snow.part set mat snow.mat}
            ; The Wii barely likes 32 particles in one particle system, let alone 300!
            #ifndef HX_WII
            {snow.part set max_parts 300}
            #else
            {snow.part set max_parts 32}
            #endif
            ; Many of these Vector2 properties are actually a range.
            ; x = min, y = max
            {snow.part set (emit_rate x) 2.0}
            {snow.part set (emit_rate y) 3.0}

            {snow.part set (speed x) 0.2}
            {snow.part set (speed y) 2}

            {snow.part set (life x) 90}
            {snow.part set (life y) 110}

            {snow.part set (start_size x) 15}
            {snow.part set (start_size y) 20}

            {snow.part set (force_dir z) -0.5}
            {snow.part set (force_dir x) -0.2}

            {snow.part set start_alpha_high 0.8}
            {snow.part set start_alpha_low 0.6}
            {snow.part set end_alpha_low 0}
            {snow.part set end_alpha_high 0}

            {snow.part set (box_extent_1 x) -1700}
            {snow.part set (box_extent_1 y) -1700}
            {snow.part set (box_extent_1 z) 0}
            {snow.part set (box_extent_2 x) 1700}
            {snow.part set (box_extent_2 y) 1700}
            {snow.part set (box_extent_2 z) 20}
            {cityscape.grp add_object snow.part}
            {$this sync_objects}

            {difference_clouds.tex set_bitmap "dx/custom_textures/_additional_textures/difference_clouds.png"}
            {difference_clouds.mat set color {pack_color 0.3 0.3 0.3}}
            {sky_dome02.mesh set_showing FALSE}
            {new Mesh sky_dome_inverted.mesh}
            {sky_dome_inverted.mesh copy sky_dome.mesh kCopyShallow}
            {sky_dome_inverted.mesh set_local_scale_index 2 -0.493}
            {sky_dome_inverted.mesh set_local_pos_index 2 -215}
            {sky_dome.mat set cull FALSE}
            {sky.grp add_object sky_dome_inverted.mesh}

            {new PropAnim wind.anim}

            {wind.anim add_keys snow.part (force_dir x) kPropFloat}
            {wind.anim add_keys snow.part (force_dir y) kPropFloat}

            {set $forcex {snow.part get (force_dir x)}}
            {set $forcey {snow.part get (force_dir y)}}
            {thread_task kTaskUISeconds (script
               {wind.anim set_key_val snow.part (force_dir x) 0 $forcex}
               {wind.anim set_key_val snow.part (force_dir y) 0 $forcey}
               {set $forcex {random_float -0.3 -0.02}}
               {set $forcey {random_float -0.05 0.05}}
               {wind.anim set_key_val snow.part (force_dir x) 1 $forcex}
               {wind.anim set_key_val snow.part (force_dir y) 1 $forcey}
               {set $animtime {random_float 10 20}}
               ;{dx_log_writer insane {sprint "Animating for " $animtime " seconds, prev: " {snow.part get (force_dir x)} " " {snow.part get (force_dir y)}}}
               {wind.anim animate (range 0 1) (period $animtime) (units kTaskUISeconds)}
               {$task sleep $animtime}
               {$task loop}
            )}
         }
      }

      {if {== $dx_splash_beta_egg FALSE}
         ;setup build label in case GoCentral is overriding our motd
         {if {! {exists dx_build.lbl}}
            {$this lbl_maker dx_build.lbl 395 -215 kBottomRight {sprint " "}}
         }
         {dx_build.lbl set width 1000}
         #ifndef RB3E
         {dx_build.lbl set_token_fmt rb3e_mod_string}
         #else
         {if_else {rb3e_allowed}
            {dx_build.lbl set_token_fmt stringify {sprint {localize rb3e_mod_string} "\n" {localize rb3e_name} " " {rb3e_build_tag}}}
            {dx_build.lbl set_token_fmt stringify {sprint {localize rb3e_mod_string} "\n" {localize rb3e_name} " " {localize unknown}}}
         }
         #endif
         {dx_build.lbl set_showing TRUE}
      }
      ;we are going to steal THE MOON
      {{{{sv8_panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 0 2.5}
      {{{{sv8_panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 2 1.3}
   )
   (dx_splash_panel_exit
      {if {exists animate_splash}
         {delete animate_splash}
      }
   )
)
