{func
   dx_set_postfx
   ($dx_postfx_state)
   ;{dx_log_writer info {sprint "func: dx_set_postfx" " - Args: " $dx_postfx_state}}
   ;{dx_log_writer info
   ;   {sprintf "Executed dx_set_postfx - time: %.4fms"
   ;      {time
            {switch $dx_postfx
               (0 ;if post proc is currently off
                  {if $dx_postfx_state ;and we want to turn it on
                     {set $dx_postfx TRUE}
                     {rnd toggle_all_postprocs}
                  }
               )
               (1 ;if post proc is currently on
                  {unless $dx_postfx_state ;and we want to turn it off
                     {set $dx_postfx FALSE}
                     {rnd toggle_all_postprocs}
                  }
               )
            }
   ;      }
   ;   }
   ;}
}

{func dx_ultra_shadows
   ($enabled)
   {if $enabled
      {ui foreach_screen $screen
         {$screen foreach_panel $panel
            {if {$panel loaded_dir}
               {{$panel loaded_dir} iterate RndDir $b
                  {$b iterate BandCamShot $d
                     {$d set self_shadow $enabled}
                     {$d set ps3_per_pixel $enabled}
                  }
                  {$b iterate BandCharacter $c
                     {$c set self_shadow $enabled}
                  }
               }
            }
         }
      }
   }
}

{func dx_air_guitar
   ($enabled)
   {if $enabled
      {ui foreach_screen $screen
         {$screen foreach_panel $panel
            {if {$panel loaded_dir}
               {{$panel loaded_dir} iterate RndDir $b
                  {$b iterate BandCharacter $c
                     {$c iterate RndDir $y
                        {if {== $y instrument}
                           {set_this $y}
                           {$this iterate Mesh $m
                              {$m set_showing FALSE}
                           }
                        }
                     }
                  }
               }
            }
         }
      }
   }
}
{func dx_char_hider
   ($enabled)
   {if {&& {exists song_mgr} $dx_mtvup {gamemode in_mode qp_coop}}
      {ui foreach_screen $screen
         {$screen foreach_panel $panel
            {if {$panel loaded_dir}
               {{$panel loaded_dir} iterate RndDir $b
                  {$b iterate BandCharacter $c
                     {do
                        ($inst {$c get instrument_type})
                        ($inst_map
                           {switch $inst
                              (keyboard keys)
                              (mic vocals)
                              $inst
                           }
                        )
                        ($song {symbol {meta_performer song}})
                        ($meta {song_mgr get_meta_data $song})
                        ($has_part {$meta has_part $inst_map})
                        ($scale {if_else $has_part 1 0})
                        {$c iterate RndDir $y
                           {set_this $y}
                           {$this iterate Mesh $m
                              {if {! {has_any_substr {$m name} (placement _base)}}
                                 {$m set_showing $has_part}
                              }
                           }
                        }
                        {if {== $inst_map drum}
                           {{$c find drum_L-stick.mesh} set_local_scale $scale $scale $scale}
                           {{$c find drum_R-stick.mesh} set_local_scale $scale $scale $scale}
                        }
                     }
                  }
               }
            }
         }
      }
   }
}
{func
   dx_noise_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_noise_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_noise_setter - time: %.4fms"
   ;      {time
            {if {! $dx_noise}
               {$world iterate PostProc $p
                  {unless
                     {||
                        {== $p "photocopy.pp"} {== $p "shitty_tv.pp"} {== $p "space_woosh.pp"} {== $p "ProFilm_mirror_a.pp"}
                        {== $p "video_security.pp"} {== $p "film_blue_filter.pp"} {== $p "video_a.pp"} {== $p "film_b+w.pp"}
                        {== $p "cameratipsover.pp"} {== $p "S3_desert.pp"} ; fix chromatic aberration
                        {== $p "tvscreen.pp"}
                     }
                     {$p set noise_intensity $dx_noise}
                  }
               }
            }
   ;      }
   ;   }
   ;}
}
{func
   dx_emulatefps_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_emulatefps_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_emulatefps_setter - time: %.4fms"
   ;      {time
            {if {!= $emulatefps 30}
               {$world iterate PostProc $p 
                  {unless {|| {== $p "photocopy.pp"} {== $p "bloom.pp"}}
                     {$p set emulate_fps $emulatefps}
                  }
               }
            }
   ;      }
   ;   }
   ;}
}
{func
   dx_motion_blur_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_motion_blur_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_motion_blur_setter - time: %.4fms"
   ;      {time
            {if {! $dx_motion_blur}
               {$world iterate PostProc $p
                  {unless
                     {||
                        {== $p "photocopy.pp"} {== $p "shitty_tv.pp"} {== $p "space_woosh.pp"} {== $p "ProFilm_mirror_a.pp"}
                        {== $p "video_security.pp"} {== $p "film_blue_filter.pp"} {== $p "video_a.pp"} {== $p "film_b+w.pp"}
                        {== $p "cameratipsover.pp"} {== $p "S3_desert.pp"} ; fix chromatic aberration
                        {== $p "tvscreen.pp"}
                     }
                     {do
                        {$p set motion_blur_velocity $dx_motion_blur}
                        {$p set motion_blur_blend $dx_motion_blur}
                        {$p set motion_blur_weight $dx_motion_blur}
                        {$p set motion_blur_exposure $dx_motion_blur}
                     }
                  }
               }
            }
   ;      }
   ;   }
   ;}
}
{func
   dx_camera_shake_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_camera_shake_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_camera_shake_setter - time: %.4fms"
   ;      {time
            #ifdef HX_WII
            {notify "dx camera shake crashes debug heads up"}
            #else
            {unless $dx_camera_shake
               ;{$char iterate CharHair $hair {$hair reset 35}}
               {$world iterate WorldDir $wd
         ;         {do
         ;            {set $dx_cursysms {system_ms}}
         ;            {set $dxdbg_objs {array ({sprint "List of BandCamShots in " {$wd name} " on " {ui current_screen} " at " $dx_cursysms "ms since boot"})}}
         ;            {$wd iterate BandCamShot $p {push_back $dxdbg_objs {sprintf "%s : %s" {$p name} {$p class_name}}}}
         ;            {write_file {sprint "GD:/dev_hdd0/game/" DX_TITLE_ID "/USRDIR/" {sprintf "%010d" $dx_cursysms} "-BandCamShots-" {$wd name} "-" {ui current_screen} ".dta"} $dxdbg_objs}
         ;         }
                  {$wd iterate BandCamShot $bcs
                     {do
                        ($dx_num_keyframes {$bcs size (keyframes)})
         ;               {dx_log_writer insane {sprint "Number of keyframes in " {$wd name} "/" {$bcs name} " on " {ui current_screen}": " $dx_num_keyframes}}
                        {foreach_int $i 0 $dx_num_keyframes
                           {$bcs set (keyframes $i shake_noisefreq) 0}
                           {$bcs set (keyframes $i shake_noiseamp) 0}
                           {$bcs set (keyframes $i shake_maxangle x) 0}
                           {$bcs set (keyframes $i shake_maxangle y) 0}
                        }
                     }
                  }
               }
            }
            #endif
   ;      }
   ;   }
   ;}
}
{func
   dx_camera_blur_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_camera_blur_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_camera_blur_setter - time: %.4fms"
   ;      {time
            {unless $dx_camera_blur
               {$world iterate WorldDir $wd
                  {$wd iterate BandCamShot $bcs
                     {do
                        ($dx_num_keyframes {$bcs size (keyframes)})
                        {foreach_int $i 0 $dx_num_keyframes
                           {$bcs set (keyframes $i blur_depth) 0}
                           {$bcs set (keyframes $i max_blur) 0}
                           {$bcs set (keyframes $i focus_blur_multiplier) 0}
                        }
                     }
                  }
               }
            }
   ;      }
   ;   }
   ;}
}
{func
   dx_force_lod_setter
   ($world)
   ;{dx_log_writer insane {sprint "func: dx_force_lod_setter"}}
   ;{dx_log_writer insane
   ;   {sprintf "Executed dx_force_lod_setter - time: %.4fms"
   ;      {time
            {if $dx_force_lod
               {$world iterate WorldDir $wd
                  {$wd iterate BandCamShot $bcs
                     {do
                        ($dx_num_targets {$bcs size (targets)})
                        {foreach_int $i 0 $dx_num_targets
                           {$bcs set (targets $i force_lod) kLOD0}
                        }
                     }
                  }
               }
            }
   ;      }
   ;   }
   ;}
}
{func
   dx_restore_online_transitions
   {cond
      ({&& {session_mgr is_local} {&& $dx_black_song_transition {! $dx_default_transitions}}}
         {do
            #ifdef HX_XBOX {set $dx_xbox_show_fallback TRUE} #endif

            {set_elem {find $syscfg vignettes interstitials preloading_screen #ifdef HX_WII pregame_screen #else game_screen #endif} 1 (TRUE ())}
            {set_elem {find $syscfg vignettes interstitials meta_loading_outro song_select_screen} 1 (TRUE ())}
         }
      )
      ({&& {session_mgr is_local} {! $dx_black_song_transition} {! $dx_default_transitions}}
         {do
            {set_elem {find $syscfg vignettes interstitials preloading_screen #ifdef HX_WII pregame_screen #else game_screen #endif} 1 {quote (1 {unquote {eval $dx_stored_outro_vignette}})}}
            {set_elem {find $syscfg vignettes interstitials meta_loading_outro song_select_screen} 1 {quote (1 {unquote {eval $dx_stored_outro_vignette}})}}
         }
      )
      ({|| {&& {== {size $dx_stored_outro_vignette} 0} {! $dx_black_song_transition}} {! {session_mgr is_local}}}
         {do
            ;{set $dx_stored_outro_vignette FALSE}
            {set_elem {find $syscfg vignettes interstitials preloading_screen #ifdef HX_WII pregame_screen #else game_screen #endif} 1 {quote ({'||' {meta_performer festival_reward} {!= {gamemode get game_screen} game_screen}} ())}}
            {set_elem {find $syscfg vignettes interstitials meta_loading_outro song_select_screen} 1 ({== {meta_performer get_venue_class} festival} ("tv4_c"))}
         }
      )
   }
}
{func dx_get_festive ($worlddir)
   {with $worlddir
      {new ParticleSys snow.part}
      {snow.part set fancy TRUE}
      {snow.part set_local_pos -1300 1300 1256}
      {new Mat snow.mat}
      {snow.mat copy star_particle.mat kCopyShallow}
      {new Tex snow.tex}
      {snow.tex set_bitmap "dx/custom_textures/_additional_textures/snow_particle.png"}
      {snow.mat set diffuse_tex snow.tex}
      {snow.part set mat snow.mat}
      ; The Wii barely likes 32 particles in one particle system, let alone 300!
      #ifndef HX_WII
      {snow.part set max_parts 300}
      #else
      {snow.part set max_parts 32}
      #endif
      ; Many of these Vector2 properties are actually a range.
      ; x = min, y = max
      {snow.part set (emit_rate x) 2.0}
      {snow.part set (emit_rate y) 3.0}

      {snow.part set (speed x) 0.2}
      {snow.part set (speed y) 2}

      {snow.part set (life x) 90}
      {snow.part set (life y) 110}

      {snow.part set (start_size x) 15}
      {snow.part set (start_size y) 20}

      {snow.part set (force_dir z) -0.5}
      {snow.part set (force_dir x) -0.2}

      {snow.part set start_alpha_high 0.8}
      {snow.part set start_alpha_low 0.6}
      {snow.part set end_alpha_low 0}
      {snow.part set end_alpha_high 0}

      {snow.part set (box_extent_1 x) -1700}
      {snow.part set (box_extent_1 y) -1700}
      {snow.part set (box_extent_1 z) 0}
      {snow.part set (box_extent_2 x) 1700}
      {snow.part set (box_extent_2 y) 1700}
      {snow.part set (box_extent_2 z) 20}
      {cityscape.grp add_object snow.part}
      {$this sync_objects}

      {difference_clouds.tex set_bitmap "dx/custom_textures/_additional_textures/difference_clouds.png"}
      {difference_clouds.mat set color {pack_color 0.3 0.3 0.3}}
      {sky_dome02.mesh set_showing FALSE}
      {new Mesh sky_dome_inverted.mesh}
      {sky_dome_inverted.mesh copy sky_dome.mesh kCopyShallow}
      {sky_dome_inverted.mesh set_local_scale_index 2 -0.493}
      {sky_dome_inverted.mesh set_local_pos_index 2 -215}
      {sky_dome.mat set cull FALSE}
      {sky.grp add_object sky_dome_inverted.mesh}

      {new PropAnim wind.anim}

      {wind.anim add_keys snow.part (force_dir x) kPropFloat}
      {wind.anim add_keys snow.part (force_dir y) kPropFloat}

      {set $forcex {snow.part get (force_dir x)}}
      {set $forcey {snow.part get (force_dir y)}}
      {thread_task kTaskUISeconds (script
         {wind.anim set_key_val snow.part (force_dir x) 0 $forcex}
         {wind.anim set_key_val snow.part (force_dir y) 0 $forcey}
         {set $forcex {random_float -0.3 -0.02}}
         {set $forcey {random_float -0.05 0.05}}
         {wind.anim set_key_val snow.part (force_dir x) 1 $forcex}
         {wind.anim set_key_val snow.part (force_dir y) 1 $forcey}
         {set $animtime {random_float 10 20}}
         ;{dx_log_writer insane {sprint "Animating for " $animtime " seconds, prev: " {snow.part get (force_dir x)} " " {snow.part get (force_dir y)}}}
         {wind.anim animate (range 0 1) (period $animtime) (units kTaskUISeconds)}
         {$task sleep $animtime}
         {$task loop}
      )}
   }
}
; Hides that crowd full of ~200 people in line at the theater, MASSIVELY improves performance
{func dx_optimize_splash ($worlddir)
   {with $worlddir
      {foreach $camshot (main.shot outro_shell.shot)
         {while {> {$camshot size (crowds)} 0}
            {$camshot remove (crowds 0)}
         }
      }
      {WorldCrowdTheater.crd set_showing FALSE}
      {WorldCrowdTheater2.crd set_showing FALSE}
   }
}
{func
   dx_sv_menu_logo_set
   ($panel)
   {if $dx_sv8_dx_vers
      {if_else $dx_event_mode
         {if_else $dx_event_logo_path
            {if_else {file_exists #ifdef HX_PS3 {sprint "GD:/dev_hdd0/game/" DX_TITLE_ID "/USRDIR/" $dx_event_logo_path} #endif #ifdef HX_XBOX {sprint "GAME:/" $dx_event_logo_path} #endif }
               {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap #ifdef HX_PS3 {sprint "GD:/dev_hdd0/game/" DX_TITLE_ID "/USRDIR/" $dx_event_logo_path} #endif #ifdef HX_XBOX {sprint "GAME:/" $dx_event_logo_path} #endif}
               {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap {sprint "dx/custom_textures/_additional_textures/logo-standard.png"}}
            }
            {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap {sprint "dx/custom_textures/_additional_textures/logo-standard.png"}}
         }
         {do
            {set $dx_main_logo_number {random_int 1 4}}
            {if {== $dx_main_logo_number 4} {set $dx_main_logo_number 1}}
            #ifndef RB3E
            {if {== $dx_main_logo_number 2} {set $dx_main_logo_number 1}}
            #endif
            {set $dx_random_number_logo {random_int 0 4096}}
            {switch $dx_random_number_logo
               (69 {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap "dx/custom_textures/_additional_textures/milo-harmonix_logo.png"})
               (420 {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap "dx/custom_textures/_additional_textures/the_game_logo.png"})
               (666 {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap "dx/custom_textures/_additional_textures/milohax-harm_logo.png"})
               {switch $dx_main_logo_number
                  ((1 3) {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap {sprint "dx/custom_textures/_additional_textures/logo-" $dx_season ".png"}})
                  (2 {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap "dx/custom_textures/_additional_textures/rb3enhanced_logo.png"})
                  kDataUnhandled
               }
            }
            {if {== $dx_season halloween}
               {{{{$panel find sv8_a} find cityscape_ao} find moon.tex} set_bitmap "dx/custom_textures/_additional_textures/blood_moon.png"}
            }
            {if {== $dx_season christmas}
               {dx_get_festive {{$panel find sv8_a} find cityscape_ao}}
            }
            ; Always optimize the splash on the menu even on console, since that crowd is mostly covered up by overshell
            {dx_optimize_splash {{$panel find sv8_a} find cityscape_ao}}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.tex} set_bitmap {sprint "dx/custom_textures/_additional_textures/logo-" $dx_season #ifdef RB3E "-rb3e" #endif ".png"}}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.mesh} set_local_pos_index 0 -750}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.mesh} set_local_rot_index 2 -30}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.mesh} set_local_scale_index 0 .45}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.mesh} set_local_scale_index 1 .35}
            {{{{$panel find sv8_a} find cityscape_ao} find logo.mesh} set_local_scale_index 2 .45}
            {{{{$panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 0 2.5}
            {{{{$panel find sv8_a} find cityscape_ao} find moon.mesh} set_local_scale_index 2 1.3}
         }
      }
   }
}