{func
   dx_bot_overdrive_handler
   {if {session_mgr is_local}
      {if {|| $dx_customizer {&& {modifier_mgr is_modifier_active mod_auto_play} {== $dx_bot_overdrive dx_on}}}
         {do
            ($tmp FALSE)
            ($player FALSE)
            ;these are set to false if an active player cannot activate
            ($act1 TRUE) ($act2 TRUE) ($act3 TRUE) ($act4 TRUE) ($act5 TRUE)
            ;if any player is deploying already, queue bots to activate
            {beatmatch foreach_active_player $player
               {if {$player is_deploying} {set $tmp TRUE}}
            }

            {foreach_int $i 1 5
               ;don't queue activate for a real player
               {if {! {eval {var {sprint "dx_bot_" $i}}}}
                  {switch $i ;i didnt want to mess with set_var on local scopes
                     ;setting 2 to false here forces bots to wait to activate until a human does
                     (1 {set $act1 FALSE} {set $act2 FALSE}) (2 {set $act1 FALSE} {set $act2 FALSE})
                     (3 {set $act1 FALSE} {set $act3 FALSE}) (4 {set $act1 FALSE} {set $act4 FALSE})
                     (5 {set $act1 FALSE} {set $act5 FALSE})
                  }
               }

               ;unqueue activations until threshold is met for active bots
               {if
                  {||
                     $dx_customizer
                     {&&
                        {>= {beatmatch num_active_players} $i}
                        {eval {var {sprint "dx_bot_" $i}}}
                     }
                  }
                  {set $player {beatmatch active_player {- $i 1}}}
                  ;if someone is already activated, activate this bot
                  {if $tmp {$player deploy_if_possible}}
                  ;don't activate if someone cant activate
                  {if {< {$player band_energy} 0.5}
                     {switch $i ;i didnt want to mess with set_var on local scopes
                        (1 {set $act1 FALSE}) (2 {set $act2 FALSE})
                        (3 {set $act3 FALSE}) (4 {set $act4 FALSE})
                        (5 {set $act5 FALSE})
                     }
                  }
               }
            }

            ;activate bots
            {foreach_int $i 1 5
               {if
                  {||
                     $dx_customizer
                     {&&
                        {>= {beatmatch num_active_players} $i}
                        {eval {var {sprint "dx_bot_" $i}}}
                     }
                  }
                  ;now that the loop is done, allow activation if up to 1 person doesnt have od
                  {if {>= {+ $act1 $act2 $act3 $act4 $act5} 4}
                     {if {== {mod $measure 4} 2}
                        {set $player {beatmatch active_player {- $i 1}}}
                        {$player deploy_if_possible}
                     }
                  }
               }
            }

         }
      }
   }
}
{func
   dx_bot_overdrive_depletor
   ; this is likely the single dumbest thing in the whole codebase ;i beg to differ
   {if {== $dx_bot_overdrive dx_drain}
      {beatmatch foreach_active_player $player {$player empty_band_energy}}
   }
}
{func
   dx_drum_overdrive_deployer
   {if_else {&& {== $dx_bot_overdrive dx_on} {modifier_mgr is_modifier_active mod_auto_play}}
      {dx_bot_overdrive_handler}
      {if {&& {== {gamemode get allow_drum_fills} FALSE} {modifier_mgr is_modifier_active mod_static_fills}}
         {beatmatch foreach_active_player $player
            {if {== {$player instrument} drum}
               ;instant activate, should revisit
               {$player deploy_if_possible}
            }
         }
      }
   }
}
{func
   dx_invalidate_scores
   {if
      {||
         {modifier_mgr is_modifier_active mod_chmode}
         {modifier_mgr is_modifier_active mod_auto_kick}
         {modifier_mgr is_modifier_active mod_fakejuke}
         {modifier_mgr is_modifier_active mod_karaoke}
         {modifier_mgr is_modifier_active mod_auto_play}
      }
      {foreach_int $i 1 5
         {if {>= {beatmatch num_active_players} $i}
            {beatmatch set_invalid_score {- $i 1}}
         }
      }
   }
}
{func
   dx_set_auto_play
   {if_else {|| $dx_customizer {modifier_mgr is_modifier_active mod_auto_play} $otherautoplay}
      {foreach_int $i 1 5
         {if {>= {beatmatch num_active_players} $i}
            {{beatmatch active_player {- $i 1}} set_auto_play
               {eval {var {sprint "dx_bot_" $i}}}
            }
         }
      }
      {beatmatch foreach_active_player $player
         {$player set_auto_play 0}
      }
   }
}